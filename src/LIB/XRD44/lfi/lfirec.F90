! Oct-2012 P. Marguinaud 64b LFI
! Jan-2011 P. Marguinaud Thread-safe LFI
SUBROUTINE LFIREC_FORT                            &
&                     (LFI, KRGPIF, KRANG, KREC )
USE LFIMOD, ONLY : LFICOM
USE PARKIND1, ONLY : JPRB
USE YOMHOOK , ONLY : LHOOK, DR_HOOK
USE LFI_PRECISION
IMPLICIT NONE
!****
!        SOUS-PROGRAMME *INTERNE* DU LOGICIEL DE FICHIERS INDEXES LFI
!     DETERMINATION DU LFI%NUMERO D'ENREGISTREMENT D'UNE PAIRE D'ARTICLES
!     D'INDEX ( LFI%NUMERO DE L'ARTICLE "NOMS", EN FAIT ) .
!**
!    ARGUMENTS : KRGPIF (ENTREE) ==> RANG DE LA P.P.I. DANS LE FICHIER;
!                KRANG  (ENTREE) ==> RANG DE L'UNITE DANS LFI%NUMERO.
!                KREC   (SORTIE) ==> LFI%NUMERO D'ENREGISTREMENT DE LA P.A.I
!
!
TYPE(LFICOM) :: LFI
INTEGER (KIND=JPLIKB) KRGPIF, KRANG, KREC, INBPIR 
INTEGER (KIND=JPLIKB) INBALO, IFACTM, INALPP
INTEGER (KIND=JPLIKB) ILARPH, INTPPI, IREP, INIMES, INUMER
!
CHARACTER(LEN=LFI%JPLSPX) CLNSPR
CHARACTER(LEN=LFI%JPLMES) CLMESS
CHARACTER(LEN=LFI%JPLFTX) CLACTI
LOGICAL LLFATA

!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
IF (LHOOK) CALL DR_HOOK('LFIREC_FORT',0,ZHOOK_HANDLE)
CLACTI=''
INBPIR=LFI%MDES1D(IXM(LFI%JPNPIR,KRANG))
INBALO=LFI%MDES1D(IXM(LFI%JPNALO,KRANG))
IFACTM=LFI%MFACTM(KRANG)
INALPP=LFI%JPNAPP*IFACTM
ILARPH=LFI%JPLARD*IFACTM
INTPPI=(INBALO-1+INALPP)/INALPP
!
IF (KRGPIF.LE.INBPIR) THEN
  KREC=2*KRGPIF
ELSEIF (KRGPIF.LE.INTPPI) THEN
!
!     CAS OU LES ARTICLES D'INDEX "RESERVES" A LA CREATION DU FICHIER
!     N'ONT PAS SUFFI A STOCKER TOUS LES DESCRIPTEURS D'ARTICLES LOGI-
!     QUES: L'EMPLACEMENT DES PAIRES D'ARTICLES D'INDEX EXCEDENTAIRES
!     EST ALORS STOCKE DANS L'ARTICLE DOCUMENTAIRE, APRES LES VALEURS
!     "UTILES" (LFI%JPLDOC MOTS), EN COMMENCANT PAR LA FIN DE CET ARTICLE.
!       ( CECI POUR MENAGER UNE EVENTUELLE AUGMENTATION DE *LFI%JPLDOC*,
!         EN CAS D'EVOLUTION DU LOGICIEL )
!
  KREC=LFI%MDES1D(IXM(ILARPH+1-(KRGPIF-INBPIR),KRANG))
ELSE
!
!          CAS OU IL Y A INCOHERENCE ENTRE TABLES ET ARGUMENTS D'APPEL
!
  KREC=0
  IREP=-16
!
!        MESSAGERIE EVENTUELLE, AVEC ABORT SI NECESSAIRE .
!
  LLFATA=LLMOER (IREP,KRANG)
!
  IF (LLFATA.OR.LFI%NIMESG.NE.0) THEN
    INIMES=2
    CLNSPR='LFIREC'
    INUMER=LFI%NUMERO(KRANG)
!
    IF (LFI%LFRANC) THEN
      WRITE (UNIT=CLMESS,FMT='(''KRGPIF='',I4,'', KRANG='',I3, &
&             '', KREC='',I6,'', CODE "INTERNE"='',I4)')        &
&        KRGPIF,KRANG,KREC,IREP
    ELSE
      WRITE (UNIT=CLMESS,FMT='(''KRGPIF='',I4,'', KRANG='',I3, &
&             '', KREC='',I6,'', "INTERNAL" CODE='',I4)')       &
&        KRGPIF,KRANG,KREC,IREP
    ENDIF
!
    CALL LFIEMS_FORT                                 &
&                   (LFI, INUMER,INIMES,IREP,LLFATA, &
&                    CLMESS,CLNSPR,CLACTI)
  ENDIF
!
ENDIF
!
IF (LHOOK) CALL DR_HOOK('LFIREC_FORT',1,ZHOOK_HANDLE)

CONTAINS

#include "lficom2.ixm.h"
#include "lficom2.llmoer.h"

END SUBROUTINE LFIREC_FORT



! Oct-2012 P. Marguinaud 64b LFI
SUBROUTINE LFIREC64             &
&           (KRGPIF, KRANG, KREC)
USE LFIMOD, ONLY : LFI => LFICOM_DEFAULT, &
&                   LFICOM_DEFAULT_INIT,   &
&                   NEW_LFI_DEFAULT
USE LFI_PRECISION
IMPLICIT NONE
! Arguments
INTEGER (KIND=JPLIKB)  KRGPIF                                 ! IN   
INTEGER (KIND=JPLIKB)  KRANG                                  ! IN   
INTEGER (KIND=JPLIKB)  KREC                                   !   OUT

IF (.NOT. LFICOM_DEFAULT_INIT) CALL NEW_LFI_DEFAULT ()

CALL LFIREC_FORT                     &
&           (LFI, KRGPIF, KRANG, KREC)

END SUBROUTINE LFIREC64

SUBROUTINE LFIREC               &
&           (KRGPIF, KRANG, KREC)
USE LFIMOD, ONLY : LFI => LFICOM_DEFAULT, &
&                   LFICOM_DEFAULT_INIT,   &
&                   NEW_LFI_DEFAULT
USE LFI_PRECISION
IMPLICIT NONE
! Arguments
INTEGER (KIND=JPLIKM)  KRGPIF                                 ! IN   
INTEGER (KIND=JPLIKM)  KRANG                                  ! IN   
INTEGER (KIND=JPLIKM)  KREC                                   !   OUT

IF (.NOT. LFICOM_DEFAULT_INIT) CALL NEW_LFI_DEFAULT ()

CALL LFIREC_MT                       &
&           (LFI, KRGPIF, KRANG, KREC)

END SUBROUTINE LFIREC

SUBROUTINE LFIREC_MT                 &
&           (LFI, KRGPIF, KRANG, KREC)
USE LFIMOD, ONLY : LFICOM
USE LFI_PRECISION
IMPLICIT NONE
! Arguments
TYPE (LFICOM)          LFI                                    ! INOUT
INTEGER (KIND=JPLIKM)  KRGPIF                                 ! IN   
INTEGER (KIND=JPLIKM)  KRANG                                  ! IN   
INTEGER (KIND=JPLIKM)  KREC                                   !   OUT
! Local integers
INTEGER (KIND=JPLIKB)  IRGPIF                                 ! IN   
INTEGER (KIND=JPLIKB)  IRANG                                  ! IN   
INTEGER (KIND=JPLIKB)  IREC                                   !   OUT
! Convert arguments

IRGPIF     = INT (    KRGPIF, JPLIKB)
IRANG      = INT (     KRANG, JPLIKB)

CALL LFIREC_FORT                     &
&           (LFI, IRGPIF, IRANG, IREC)

KREC       = INT (      IREC, JPLIKM)

END SUBROUTINE LFIREC_MT

!INTF KRGPIF        IN    
!INTF KRANG         IN    
!INTF KREC            OUT 
