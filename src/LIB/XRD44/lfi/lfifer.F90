! Oct-2012 P. Marguinaud 64b LFI
! Jan-2011 P. Marguinaud Thread-safe LFI
! Sep-2012 P. Marguinaud Fix uninitialized variables

SUBROUTINE LFIFER_FORT                             &
&                     (LFI, KREP, KNUMER, CDSTTC )
USE LFIMOD, ONLY : LFICOM
USE PARKIND1, ONLY : JPRB
USE YOMHOOK , ONLY : LHOOK, DR_HOOK
USE LFI_PRECISION
IMPLICIT NONE
!****
!        SOUS-PROGRAMME DE FERMETURE D'UN FICHIER INDEXE AU SENS DU
!     LOGICIEL DE FICHIERS INDEXES LFI.
!        CETTE FERMETURE EST VITALE SI LE FICHIER A ETE MODIFIE DEPUIS
!     LA DERNIERE OUVERTURE, ET EST DE TOUTE MANIERE CHAUDEMENT
!     RECOMMANDEE AVANT DE SORTIR DU PROGRAMME UTILISATEUR.
!
!        De maniere generale, il vaut mieux fermer une unite logique
!     des que l'on n'en a plus besoin, de maniere a ne pas bloquer un
!     espace dans les tables qui pourrait faire defaut lors d'une
!     ouverture ulterieure, particulierement pour un fichier "multiple"
!     qui a besoin d'espace CONTIGU dans les tables des unites logiques.
!     Par ailleurs, tout fichier ouvert (au sens FORTRAN du terme cette
!     fois) "occupe" generalement un morceau de la memoire du programme.
!**
!    ARGUMENTS : KREP   (SORTIE) ==> CODE-REPONSE DU SOUS-PROGRAMME;
!                KNUMER (ENTREE) ==> LFI%NUMERO DE L'UNITE LOGIQUE;
!                CDSTTC (ENTREE) ==> "STATUS" EVENTUEL POUR "CLOSE".
!
!    Modifications:
!
!    02/06/97, Jean Clochard.
!
!              -Modification des impressions pour que l'annee puisse
!               etre imprimee avec 4 chiffres.
!
!
TYPE(LFICOM) :: LFI
CHARACTER (LEN=*)          CDSTTC
CHARACTER (LEN=LFI%JPLSTX) CLSTTC
CHARACTER (LEN=3)          CLAUXI
!
INTEGER (KIND=JPLIKB) ITAMPO (LFI%JPLARX)
INTEGER (KIND=JPLIKB) KREP, KNUMER 
INTEGER (KIND=JPLIKB) IEXPLO (LFI%JPNPIA+LFI%JPNPIS)
INTEGER (KIND=JPLIKB) ILSTTU, IREPX, INAPHY, IRANG, IREP 
INTEGER (KIND=JPLIKB) ILSTTC, IREC, INPPIM
INTEGER (KIND=JPLIKB) IFACTM, ILARPH, INALPP, INBALO, INTPPI
INTEGER (KIND=JPLIKB) INTRLZ, IDECAL, J
INTEGER (KIND=JPLIKB) IPOTZC, IRGPFC, IRGPMC, ILFORC, INPILE
INTEGER (KIND=JPLIKB) INIMES, IPOTZS, JJ
INTEGER (KIND=JPLIKB) IRGPFS, IPOTZE, IRGPMS, INTCON, IPOSFE
INTEGER (KIND=JPLIKB) JNPAGE, IRGPFE, JR
INTEGER (KIND=JPLIKB) IRGPME, IRANGM, IDECDB, INBARE, IRGPC2
INTEGER (KIND=JPLIKB) IRGPS2, IDECDC
INTEGER (KIND=JPLIKB) INBARC, IDECDS, INBARS, INBART, INTPPN
INTEGER (KIND=JPLIKB) ILOMIN, ILOMAX
INTEGER (KIND=JPLIKB) IRPIFN, INPIME, IDEBEX, IRGPIM, IRGPIF
INTEGER (KIND=JPLIKB) INLNOM, IDEBUT
INTEGER (KIND=JPLIKB) ILONGA, INALDO, IPOSNU, IRANIE, IRETIN
INTEGER (KIND=JPLIKB) IAUXIL
!
LOGICAL LLSTTU, LLVERF, LLVERG, LLECRD, LLIMST
!
CHARACTER(LEN=LFI%JPLSPX) CLNSPR
CHARACTER(LEN=LFI%JPLMES) CLMESS
CHARACTER(LEN=LFI%JPLFTX) CLACTI
LOGICAL LLFATA

!**
!     1.  -  CONTROLES DES PARAMETRES D'APPEL, PUIS INITIALISATIONS.
!-----------------------------------------------------------------------
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
IF (LHOOK) CALL DR_HOOK('LFIFER_FORT',0,ZHOOK_HANDLE)
CLACTI=''
ITAMPO=0

IF (LFI%LMISOP) WRITE (UNIT=LFI%NULOUT,FMT=*)'DEBUT LFIFER'
CLACTI=''
CLNSPR='LFIFER'
ILSTTU=MIN (INT (LEN (CLSTTC), JPLIKB),  &
&            INT (LEN (CDSTTC), JPLIKB))
CLSTTC=CDSTTC(:ILSTTU)
IREPX=0
INAPHY=0
LLVERF=.FALSE.
LLVERG=.FALSE.
CALL LFINUM_FORT                    &
&               (LFI, KNUMER,IRANG)
!
IF (IRANG.EQ.0) THEN
  IREP=-1
  GOTO 1001
ENDIF
!
 IF (LFI%LMULTI) CALL LFIVER_FORT                              &
&                                (LFI, LFI%VERRUE(IRANG),'ON')
LLVERF=LFI%LMULTI
!
IF (CDSTTC.EQ.'KEEP'.AND.LFI%CSTAOP(IRANG).EQ.'SCRATCH') THEN
  IREP=-19
  LLFATA=LLMOER (IREP,IRANG)
!
  IF (LLFATA) THEN
    GOTO 1001
  ELSE
!
!        SI L'ERREUR (-19) N'EST PAS FATALE, ON FERME L'UNITE LOGIQUE,
!       MAIS SANS PRECISER DE DIRECTIVE "STATUS" DANS LE "CLOSE".
!
    LLSTTU=.FALSE.
    GOTO 105
  ENDIF
!
ELSE
  LLSTTU=CDSTTC.EQ.'KEEP'.OR.CDSTTC.EQ.'DELETE'
!
  IF (LLSTTU) THEN
    ILSTTC=INT (INDEX (CDSTTC,' '), JPLIKB)-1
    IF (ILSTTC.GT.0) ILSTTU=ILSTTC
    CLSTTC=CDSTTC(:ILSTTU)
  ELSE
    CLSTTC=LFI%CHINCO(:ILSTTU)
  ENDIF
!
ENDIF
!
IREP=0
!
105 CONTINUE
IREPX=IREP
INPPIM=LFI%NPPIMM(IRANG)
IFACTM=LFI%MFACTM(IRANG)
ILARPH=LFI%JPLARD*IFACTM
INALPP=LFI%JPNAPP*IFACTM
INBALO=LFI%MDES1D(IXM(LFI%JPNALO,IRANG))
INTPPI=(INBALO-1+INALPP)/INALPP
!**
!     2.  -  TRAITEMENT D'EVENTUELS "TROUS" D'INDEX DE LONGUEUR ZERO.
!            CES "PARASITES" PEUVENT AVOIR ETE CREES PAR LE RECYCLAGE
!            DE TROUS DANS LE FICHIER; ON VA LES SUPPRIMER.
!
!         L'ALGORITHME CHOISI N'EST PEUT-ETRE PAS LE PLUS EFFICACE;
!     MAIS A PARTIR DU MOMENT OU LA PROBABILITE DE GENERER DES TROUS
!     DE LONGUEUR NULLE EST FAIBLE, ET OU ON NE LES RETASSE
!     QU'A LA FERMETURE, IL N'Y A PAS LIEU DE RAFFINER A OUTRANCE !
!-----------------------------------------------------------------------
!
INTRLZ=LFI%NTRULZ(IRANG)
IF (INTRLZ.EQ.0) GOTO 300
!
IDECAL=0
IPOTZC=LFI%NRFPTZ(IRANG)
IRGPFC=1+(IPOTZC-1)/INALPP
!*
!     2.1 - MISE EN MEMOIRE DE LA PREMIERE P.A.I. AYANT UN TEL TROU.
!-----------------------------------------------------------------------
!
DO J=1,INPPIM
IRGPMC=LFI%MRGPIM(J,IRANG)
!
IF (LFI%MRGPIF(IRGPMC).EQ.IRGPFC) THEN
!
IF (.NOT.LFI%LPHASP(IRGPMC)) THEN
!
  CALL LFIPHA_FORT                                &
&                 (LFI, IREP,IRANG,IRGPMC,IRETIN)
!
    IF (IRETIN.EQ.1) THEN
      GOTO 903
    ELSEIF (IRETIN.EQ.2) THEN
      GOTO 904
    ELSEIF (IRETIN.NE.0) THEN
      GOTO 1001
    ENDIF
!
  ENDIF
!
  GOTO 220
!
ENDIF
!
ENDDO
!
ILFORC=IRGPFC+1
INPILE=2
CALL LFIPIM_FORT                                       &
&               (LFI, IREP,IRANG,IRANGM,IRGPMC,IRGPFC, &
&                ILFORC,INPILE,IRETIN)
!
IF (IRETIN.EQ.1) THEN
  GOTO 903
ELSEIF (IRETIN.EQ.2) THEN
  GOTO 904
ELSEIF (IRETIN.NE.0) THEN
  GOTO 1001
ENDIF
!
INPPIM=MAX (INPPIM,IRANGM)
!
220 CONTINUE
!*
!     2.2 - AMORCAGE DE LA RECHERCHE DU NOMBRE DE TROUS NULS CONSECUTIFS
!           ET DE L'EVENTUEL TROU NUL "SUIVANT" CE PAQUET.
!-----------------------------------------------------------------------
!
IPOTZS=IPOTZC+IDECAL
IRGPFS=1+(IPOTZS-1)/INALPP
IPOTZE=IPOTZS
IRGPMS=0
!
!       ON ELIMINE LES CAS "TRIVIAUX"
!
IF (INTRLZ.EQ.1.OR.IPOTZE+INTRLZ.GT.INBALO) THEN
  INTCON=INTRLZ
  IPOSFE=INBALO
  GOTO 250
ENDIF
!*
!     2.3 - DECOMPTE DU NOMBRE DE TROUS NULS CONSECUTIFS.
!-----------------------------------------------------------------------
!
DO JNPAGE=IRGPFS,INTPPI
IRGPFE=JNPAGE
!
DO J=1,INPPIM
IRGPME=LFI%MRGPIM(J,IRANG)
!
IF (LFI%MRGPIF(IRGPME).EQ.IRGPFE) THEN
!
  IF (.NOT.LFI%LPHASP(IRGPME)) THEN
!
    CALL LFIPHA_FORT                                &
&                   (LFI, IREP,IRANG,IRGPME,IRETIN)
!
    IF (IRETIN.EQ.1) THEN
      GOTO 903
    ELSEIF (IRETIN.EQ.2) THEN
      GOTO 904
    ELSEIF (IRETIN.NE.0) THEN
      GOTO 1001
    ENDIF
!
  ENDIF
!
  GOTO 233
!
ENDIF
!
ENDDO
!
INPILE=2
CALL LFIPIM_FORT                                &
&               (LFI, IREP,IRANG,IRANGM,IRGPME, &
&                IRGPFE,IRGPFC,INPILE,IRETIN)
!
IF (IRETIN.EQ.1) THEN
  GOTO 903
ELSEIF (IRETIN.EQ.2) THEN
  GOTO 904
ELSEIF (IRETIN.NE.0) THEN
  GOTO 1001
ENDIF
!
INPPIM=MAX (INPPIM,IRANGM)
!
233 CONTINUE
IF (IRGPMS.EQ.0) IRGPMS=IRGPME
IDECDB=IPOTZE-1-(IRGPFE-1)*INALPP
INBARE=MIN (INBALO,IRGPFE*INALPP)-IPOTZE+1
INTCON=0
!
DO J=1,INBARE
!
IF (LFI%MLGPOS(IXM(2*(J+IDECDB)-1,IRGPME)).NE.0) THEN
  INTCON=INTCON+J-1
  GOTO 240
ENDIF
!
ENDDO
!
INTCON=INTCON+INBARE
IPOTZE=IPOTZE+INBARE
ENDDO
!*
!     2.4 - RECHERCHE DU PROCHAIN TROU NUL, APRES LE PAQUET TROUVE.
!-----------------------------------------------------------------------
!
240 CONTINUE
IPOTZE=IPOTZE+INTCON
!
!             D'ABORD, ELIMINATION DES CAS "TRIVIAUX"
!
IF (INTCON.EQ.INTRLZ) THEN
!
!        ON A TROUVE TOUS LES TROUS NULS RESTANT.
!
  IPOSFE=INBALO
ELSEIF (INTCON.EQ.(INTRLZ-1)) THEN
!
!        PLUS QU'UN SEUL TROU NUL... CE SERA DONC CELUI DE RANG MAXIMUM.
!
  IPOSFE=LFI%NRFDTZ(IRANG)-1
ELSE
!
!          CAS GENERAL.
!
  DO JNPAGE=IRGPFE,INTPPI
!
  IF (JNPAGE.NE.IRGPFE) THEN
!
    DO J=1,INPPIM
    IRGPME=LFI%MRGPIM(J,IRANG)
!
    IF (LFI%MRGPIF(IRGPME).EQ.JNPAGE) THEN
!
      IF (.NOT.LFI%LPHASP(IRGPME)) THEN
!
        CALL LFIPHA_FORT                                &
&                       (LFI, IREP,IRANG,IRGPME,IRETIN)
!
        IF (IRETIN.EQ.1) THEN
          GOTO 903
        ELSEIF (IRETIN.EQ.2) THEN
          GOTO 904
        ELSEIF (IRETIN.NE.0) THEN
          GOTO 1001
        ENDIF
!
      ENDIF
!
      GOTO 243
!
    ENDIF
!
    ENDDO
!
    INPILE=2
    CALL LFIPIM_FORT                                       &
&                   (LFI, IREP,IRANG,IRANGM,IRGPME,JNPAGE, &
&                    IRGPFC,INPILE,IRETIN)
!
    IF (IRETIN.EQ.1) THEN
      GOTO 903
    ELSEIF (IRETIN.EQ.2) THEN
      GOTO 904
    ELSEIF (IRETIN.NE.0) THEN
      GOTO 1001
    ENDIF
!
    INPPIM=MAX (INPPIM,IRANGM)
  ENDIF
!
243 CONTINUE
  IDECDB=IPOTZE-1-(JNPAGE-1)*INALPP
  INBARE=MIN (INBALO,JNPAGE*INALPP)-IPOTZE+1
!
  DO J=1,INBARE
!
  IF (LFI%MLGPOS(IXM(2*(J+IDECDB)-1,IRGPME)).EQ.0) THEN
    IPOSFE=(JNPAGE-1)*INALPP+J-1
    GOTO 250
  ENDIF
!
  ENDDO
!
  ENDDO
!
ENDIF
!
250 CONTINUE
!*
!     2.5 - RETASSAGE "A GAUCHE" D'UNE PARTIE DE L'INDEX.
!-----------------------------------------------------------------------
!
IDECAL=IDECAL+INTCON
!
251 CONTINUE
IPOTZS=IPOTZC+IDECAL
IRGPC2=1+(IPOTZC-1)/INALPP
IRGPS2=1+(IPOTZS-1)/INALPP
!
IF (IRGPC2.NE.IRGPFC) THEN
  IRGPFC=IRGPC2
!
  DO J=2,INPPIM
  IRGPMC=LFI%MRGPIM(J,IRANG)
!
  IF (LFI%MRGPIF(IRGPMC).EQ.IRGPFC) THEN
!
    IF (.NOT.LFI%LPHASP(IRGPMC)) THEN
!
      CALL LFIPHA_FORT                                &
&                     (LFI, IREP,IRANG,IRGPMC,IRETIN)
!
      IF (IRETIN.EQ.1) THEN
        GOTO 903
      ELSEIF (IRETIN.EQ.2) THEN
        GOTO 904
      ELSEIF (IRETIN.NE.0) THEN
        GOTO 1001
      ENDIF
!
    ENDIF
!
    GOTO 254
!
  ENDIF
!
  ENDDO
!
  INPILE=2
  CALL LFIPIM_FORT                                       &
&                 (LFI, IREP,IRANG,IRANGM,IRGPMC,IRGPFC, &
&                  IRGPS2,INPILE,IRETIN)
!
  IF (IRETIN.EQ.1) THEN
    GOTO 903
  ELSEIF (IRETIN.EQ.2) THEN
    GOTO 904
  ELSEIF (IRETIN.NE.0) THEN
    GOTO 1001
  ENDIF
!
  INPPIM=MAX (INPPIM,IRANGM)
ENDIF
!
254 CONTINUE
!
IF (IRGPS2.NE.IRGPFS) THEN
  IRGPFS=IRGPS2
!
  DO J=2,INPPIM
  IRGPMS=LFI%MRGPIM(J,IRANG)
!
  IF (LFI%MRGPIF(IRGPMS).EQ.IRGPFS) THEN
!
    IF (.NOT.LFI%LPHASP(IRGPMS)) THEN
!
      CALL LFIPHA_FORT                                &
&                     (LFI, IREP,IRANG,IRGPMS,IRETIN)
!
      IF (IRETIN.EQ.1) THEN
        GOTO 903
      ELSEIF (IRETIN.EQ.2) THEN
        GOTO 904
      ELSEIF (IRETIN.NE.0) THEN
        GOTO 1001
      ENDIF
!
    ENDIF
!
    GOTO 257
!
  ENDIF
!
  ENDDO
!
  INPILE=2
  CALL LFIPIM_FORT                                       &
&                 (LFI, IREP,IRANG,IRANGM,IRGPMS,IRGPFS, &
&                  IRGPFC,INPILE,IRETIN)
!
  IF (IRETIN.EQ.1) THEN
    GOTO 903
  ELSEIF (IRETIN.EQ.2) THEN
    GOTO 904
  ELSEIF (IRETIN.NE.0) THEN
    GOTO 1001
  ENDIF
!
  INPPIM=MAX (INPPIM,IRANGM)
ENDIF
!
257 CONTINUE
IDECDC=IPOTZC-1+(IRGPFC-1)/INALPP
INBARC=MIN (INBALO-IDECAL,IRGPFC*INALPP)-IPOTZC+1
IDECDS=IPOTZS-1+(IRGPFS-1)/INALPP
INBARS=MIN (INBALO,IRGPFS*INALPP)-IPOTZS+1
INBART=MIN (INBARC,INBARS)
!
DO J=1,INBART
LFI%CNOMAR(IXC(IDECDC+J,IRGPMC))=LFI%CNOMAR(IXC(IDECDS+J,IRGPMS))
ENDDO
!
DO J=1,2*INBART
LFI%MLGPOS(IXM(2*IDECDC+J,IRGPMC))=                   &
&                   LFI%MLGPOS(IXM(2*IDECDS+J,IRGPMS))
ENDDO
!
IPOTZC=IPOTZC+INBART
!
IF (IPOTZS+INBART.LT.IPOSFE) THEN
  GOTO 251
ELSEIF (IPOSFE.LT.INBALO) THEN
  GOTO 220
ENDIF
!*
!     2.6 - MISE A JOUR DE CERTAINES TABLES.
!-----------------------------------------------------------------------
!
LFI%NBTROU(IRANG)=LFI%NBTROU(IRANG)-IDECAL
INBALO=INBALO-IDECAL
LFI%MDES1D(IXM(LFI%JPNALO,IRANG))=INBALO
INTPPN=(INBALO-1+INALPP)/INALPP
LFI%NALDPI(IRANG)=INBALO-(INTPPN-1)*INALPP
!
IF (INTPPN.NE.INTPPI) THEN
!
!           ON A DONC LAISSE DES P.P.I. "PARASITES" EN MEMOIRE.
!        A PRIORI CE N'EST PAS GENANT, CAR ON VA BIENTOT RELACHER
!        TOUTES CELLES RELATIVES AU FICHIER QUE L'ON TRAITE.
!        NEANMOINS IL EST PREFERABLE DE REDEFINIR LE RANG EN MEMOIRE
!        DE LA DERNIERE P.P.I. DU FICHIER.
!
  LFI%NPODPI(IRANG)=IRGPMC
  INTPPI=INTPPN
ENDIF
!
300 CONTINUE
!**
!     3.  -  CAS OU IL FAUT RECALCULER LES LONGUEURS EXTREMES DES
!            ARTICLES DE DONNEES.
!-----------------------------------------------------------------------
!
IF (.NOT.LFI%LMIMAL(IRANG)) GOTO 400
!*
!     3.1 - EXPLORATION DES P.P.I., PUIS EVENTUELLEMENT DES P.A.I. .
!-----------------------------------------------------------------------
!
ILOMIN=0
ILOMAX=0
IRPIFN=1
INPIME=0
!
IF (LFI%NPODPI(IRANG).EQ.2) THEN
  IDEBEX=3
ELSE
  IDEBEX=2
ENDIF
!
DO JNPAGE=1,INTPPI
!
IF (JNPAGE.LE.INPPIM) THEN
!
!           IL S'AGIT D'UNE EXPLORATION EN MEMOIRE ( PAGES D'INDEX ).
!
  IRGPIM=LFI%MRGPIM(JNPAGE,IRANG)
  IRGPIF=LFI%MRGPIF(IRGPIM)
  INPIME=INPIME+1
  IEXPLO(INPIME)=IRGPIF
  IF (IRGPIF.EQ.(IRPIFN+1)) IRPIFN=IRGPIF
!
  IF (.NOT.LFI%LPHASP(IRGPIM)) THEN
!
    CALL LFIPHA_FORT                                &
&                   (LFI, IREP,IRANG,IRGPIM,IRETIN)
!
    IF (IRETIN.EQ.1) THEN
      GOTO 903
    ELSEIF (IRETIN.EQ.2) THEN
      GOTO 904
    ELSEIF (IRETIN.NE.0) THEN
      GOTO 1001
    ENDIF
!
  ENDIF
!
ELSE
!
!           IL S'AGIT D'UNE EXPLORATION "HORS MEMOIRE";
!         ON CHERCHE LA PROCHAINE P.A.I. NON EXPLOREE .
!
  IF (JNPAGE.EQ.INPPIM+1) IRGPIF=IRPIFN
!
311 CONTINUE
  IRGPIF=IRGPIF+1
!
  DO J=IDEBEX,INPIME
  IF (IEXPLO(J).EQ.IRGPIF) GOTO 311
  ENDDO
!
  ILFORC=1
  INPILE=2
  CALL LFIPIM_FORT                                       &
&                 (LFI, IREP,IRANG,IRANGM,IRGPIM,IRGPIF, &
&                  ILFORC,INPILE,IRETIN)
!
  IF (IRETIN.EQ.1) THEN
    GOTO 903
  ELSEIF (IRETIN.EQ.2) THEN
    GOTO 904
  ELSEIF (IRETIN.NE.0) THEN
    GOTO 1001
  ENDIF
!
ENDIF
!
INBART=MIN (INALPP,INBALO-(IRGPIF-1)*INALPP)
!
IF (ILOMIN.EQ.0) THEN
!
  DO J=1,INBART
!
  IF (LFI%CNOMAR(IXC(J,IRGPIM)).NE.' ') THEN
    ILOMIN=LFI%MLGPOS(IXM(2*J-1,IRGPIM))
    ILOMAX=ILOMIN
    IDEBUT=J+1
    GOTO 315
  ENDIF
!
  ENDDO
!
  CYCLE
ELSE
  IDEBUT=1
ENDIF
!
315 CONTINUE
!
DO J=IDEBUT,INBART
!
IF (LFI%CNOMAR(IXC(J,IRGPIM)).NE.' ') THEN
  ILONGA=LFI%MLGPOS(IXM(2*J-1,IRGPIM))
  ILOMIN=MIN (ILONGA,ILOMIN)
  ILOMAX=MAX (ILONGA,ILOMAX)
ENDIF
!
ENDDO
!
ENDDO   
!*
!     3.2 - MISE A JOUR DES TABLES CONCERNEES.
!-----------------------------------------------------------------------
!
LFI%MDES1D(IXM(LFI%JPLNAL,IRANG))=ILOMIN
LFI%MDES1D(IXM(LFI%JPLXAL,IRANG))=ILOMAX
!
400 CONTINUE
!**
!     4.  -  "VIDAGE" SUR FICHIER DES PAGES RESTANT A ECRIRE.
!-----------------------------------------------------------------------
!*
!     4.1 -  PAGES DE *DONNEES* RESTANT A ECRIRE.
!-----------------------------------------------------------------------
!
DO J=0,LFI%JPNPDF-1
!
IF (LFI%LECRPD(J,IRANG)) THEN
!
  CALL LFIVID_FORT                                     &
&                 (LFI, IREP,IRANG,J,ITAMPO(1),IRETIN)
!
  IF (IRETIN.EQ.1) THEN
    GOTO 903
  ELSEIF (IRETIN.EQ.2) THEN
    GOTO 904
  ELSEIF (IRETIN.NE.0) THEN
    GOTO 1001
  ENDIF
!
ENDIF
!
ENDDO
!*
!     4.2 -  (PAIRES DE) PAGES D'*INDEX* RESTANT A ECRIRE.
!-----------------------------------------------------------------------
!
INPPIM=LFI%NPPIMM(IRANG)
!
DO J=1,INPPIM
IRGPIM=LFI%MRGPIM(J,IRANG)
IRGPIF=LFI%MRGPIF(IRGPIM)
CALL LFIREC_FORT                         &
&               (LFI, IRGPIF,IRANG,IREC)
!
IF (LFI%LECRPI(IRGPIM,1)) THEN
!
  IF (J.EQ.LFI%NPODPI(IRANG).AND.LFI%NALDPI(IRANG).NE.INALPP) THEN
!
!          COMPLEMENT DE LA DERNIERE PAGE D'INDEX NOMS AVEC UN NOM
!        CONVENTIONNEL.
!
    DO JJ=LFI%NALDPI(IRANG)+1,LFI%JPNXNA*IFACTM
    LFI%CNOMAR(IXC(JJ,IRGPIM))='**FIN D''INDEX**'
    ENDDO
!
  ENDIF
!
  INAPHY=IREC
  CALL LFIECC_FORT                                      &
&                 (LFI, IREP,KNUMER,IREC,             &
&                  LFI%CNOMAR(IXC(1_JPLIKB ,IRGPIM)), &
&                  LFI%NBWRIT(IRANG),IFACTM,          &
&                  LFI%YLFIC (IRANG),IRETIN)
!
  IF (IRETIN.EQ.1) THEN
    GOTO 903
  ELSEIF (IRETIN.NE.0) THEN
    GOTO 1001
  ENDIF
!
  LFI%LECRPI(IRGPIM,1)=.FALSE.
ENDIF
!
IF (LFI%LECRPI(IRGPIM,2).AND.LFI%LPHASP(IRGPIM)) THEN
!
  IF (J.EQ.LFI%NPODPI(IRANG).AND.LFI%NALDPI(IRANG).NE.INALPP) THEN
!
!          COMPLEMENT DE LA DERNIERE PAGE D'INDEX LONGUEUR/POSITION
!        AVEC DES ZEROS.
!
    DO JJ=2*LFI%NALDPI(IRANG)+1,ILARPH
    LFI%MLGPOS(IXM(JJ,IRGPIM))=0
    ENDDO
!
  ENDIF
!
  INAPHY=IREC+1
  CALL LFIEDO_FORT                                      &
&                 (LFI, IREP,KNUMER,IREC+1,           &
&                  LFI%MLGPOS(IXM(1_JPLIKB ,IRGPIM)), &
&                  LFI%NBWRIT(IRANG),IFACTM,          &
&                  LFI%YLFIC (IRANG),IRETIN)
!
  IF (IRETIN.EQ.1) THEN
    GOTO 903
  ELSEIF (IRETIN.NE.0) THEN
    GOTO 1001
  ENDIF
!
  LFI%LECRPI(IRGPIM,2)=.FALSE.
ENDIF
!
ENDDO
!*
!     4.3 -  CAS DE L'ARTICLE DOCUMENTAIRE, SI NECESSAIRE.
!-----------------------------------------------------------------------
!
LLECRD=LFI%LMODIF(IRANG).OR.LFI%NBWRIT(IRANG).NE.0
!
IF (LLECRD) THEN
!
!        AU PREALABLE, MISE A JOUR DE CET ARTICLE DOCUMENTAIRE.
!
  IAUXIL=IXM(LFI%JPNRES,IRANG)
  LFI%MDES1D(IAUXIL)=LFI%MDES1D(IAUXIL)+LFI%NREESP(IRANG)
  IAUXIL=IXM(LFI%JPNREC,IRANG)
  LFI%MDES1D(IAUXIL)=LFI%MDES1D(IAUXIL)+LFI%NREECO(IRANG)
  IAUXIL=IXM(LFI%JPNREL,IRANG)
  LFI%MDES1D(IAUXIL)=LFI%MDES1D(IAUXIL)+LFI%NREELO(IRANG)
  IAUXIL=IXM(LFI%JPNTRU,IRANG)
  LFI%MDES1D(IAUXIL)=LFI%MDES1D(IAUXIL)+LFI%NBTROU(IRANG)
  CALL LFIDAH_FORT                                         &
&                 (LFI, LFI%MDES1D(IXM(LFI%JPDDMG,IRANG)), &
&               LFI%MDES1D(IXM(LFI%JPHDMG,IRANG)))
  IREC=1
  INAPHY=IREC
  CALL LFIEDO_FORT                                     &
&                 (LFI, IREP,KNUMER,IREC,            &
&                  LFI%MDES1D(IXM(1_JPLIKB ,IRANG)), &
&                  LFI%NBWRIT(IRANG),IFACTM,         &
&                  LFI%YLFIC (IRANG),IRETIN)
!
  IF (IRETIN.EQ.1) THEN
    GOTO 903
  ELSEIF (IRETIN.NE.0) THEN
    GOTO 1001
  ENDIF
!
ENDIF
!**
!     5.  -  FERMETURE EFFECTIVE (*CLOSE*) DU FICHIER.
!-----------------------------------------------------------------------
!
INALDO=INBALO-LFI%MDES1D(IXM(LFI%JPNTRU,IRANG))
INAPHY=0
!
IF (LLSTTU) THEN
  IF (KNUMER < 0) THEN
    CALL CLOSEC (IREP, CLSTTC)
    IF (IREP /= 0) GOTO 905
  ELSE
    CLOSE (UNIT=KNUMER,STATUS=CLSTTC,ERR=905,IOSTAT=IREP)
  ENDIF
ELSE
!
  IF (INALDO.EQ.0) THEN
!
!       SI ON SE RETROUVE AVEC UN FICHIER "VIDE" ET QUE L'ON N'A PAS DE
!       PARAMETRE "STATUS" POUR LE "CLOSE", ON VA ESSAYER DE RELACHER LE
!       FICHIER, AFIN DE NE PAS LAISSER TRAINER UN TEL FICHIER "ZOMBIE".
!             ( "VIDE" = SANS ARTICLE LOGIQUE DE DONNEES )
!       ON N'A PAS DE GARANTIE D'Y ARRIVER, DANS LA MESURE OU ON N'EST
!       PAS SUR D'AVOIR DES DROITS D'ACCES SUFFISANTS.
!
    IF (KNUMER < 0) THEN
      CALL CLOSEC (IREP, 'DELETE')
      IF (IREP /= 0) GOTO 511
    ELSE
      CLOSE (UNIT=KNUMER,STATUS='DELETE',ERR=511)
    ENDIF
    CLSTTC='DELETE'
    LLSTTU=.TRUE.
    GOTO 600
  ENDIF
!
511 CONTINUE
!
IF (KNUMER < 0) THEN
  CALL CLOSEC (IREP, 'KEEP')
  IF (IREP /= 0) GOTO 905
ELSE
  CLOSE (UNIT=KNUMER,ERR=905,IOSTAT=IREP)
ENDIF
!
ENDIF
!
600 CONTINUE
!**
!     6.  -  IMPRESSION EVENTUELLE DE STATISTIQUES D'UTILISATION.
!-----------------------------------------------------------------------
!
LFI%NDEROP(IRANG)=9
LFI%NDERCO(IRANG)=IREP
LLIMST=LFI%NISTAG.EQ.2.OR.(LFI%NISTAG.EQ.1.AND.LFI%LISTAT(IRANG))
IF (LLIMST) CALL LFIIST_FORT                    &
&                           (LFI, IRANG,.TRUE.)
!**
!     7.  -  "NETTOYAGE" DES TABLES AYANT PERMIS DE GERER LE FICHIER.
!            ( AU MOINS CELLES AYANT UN CARACTERE "GLOBAL"; A NOTER
!              TOUTEFOIS QU'ON NE TOUCHE PAS AUX CARACTERISTIQUES DES
!              PAGES D'INDEX PREAFFECTEES )
!-----------------------------------------------------------------------
!
 IF (LFI%LMULTI) CALL LFIVER_FORT                       &
&                                (LFI, LFI%VERGLA,'ON')
LLVERG=LFI%LMULTI
!
DO J=2,MIN (INPPIM,LFI%JPNPIA)
IRGPIM=LFI%MRGPIM(J,IRANG)
!
DO JR=IRGPIM,IRGPIM+IFACTM-1
LFI%MRGPIF(JR)=LFI%JPNIL
ENDDO
!
ENDDO
!
DO J=LFI%JPNPIA+1,INPPIM
IRGPIM=LFI%MRGPIM(J,IRANG)
!
DO JR=IRGPIM,IRGPIM+IFACTM-1
LFI%MCOPIF(JR)=LFI%JPNIL
LFI%MRGPIF(JR)=LFI%JPNIL
ENDDO
!
ENDDO
!
LFI%NPISAF=LFI%NPISAF-MAX (0_JPLIKB ,(INPPIM-LFI%JPNPIA)*IFACTM)
!
DO JR=IRANG,IRANG+IFACTM-1
LFI%NUMERO(JR)=LFI%JPNIL
ENDDO
!
DO J=1,LFI%NBFIOU
!
IF (LFI%NUMIND(J).EQ.IRANG) THEN
  IPOSNU=J
  GOTO 707
ENDIF
!
ENDDO
!
IREP=-16
GOTO 1001
!
707 CONTINUE
!
LFI%NBFIOU=LFI%NBFIOU-1
LFI%NFACTM=LFI%NFACTM-IFACTM
!
DO J=IPOSNU,LFI%NBFIOU
LFI%NUMIND(J)=LFI%NUMIND(J+1)
ENDDO
!*
!     7.1 -  ASPECTS SPECIFIQUES AUX TABLES D'IMPORT/EXPORT.
!-----------------------------------------------------------------------
!
IF (LFI%NEXPOR(IRANG).GT.0.OR.LFI%NIMPOR(IRANG).GT.0) THEN
  IRANIE=MAX (LFI%NEXPOR(IRANG),LFI%NIMPOR(IRANG))
  INIMES=IXNIMS (IRANG)
!
  IF (INIMES.GE.1) THEN
    WRITE (UNIT=CLMESS,FMT='(''KNUMER='',I3,                    &
&           '', ATTENTION: IMPORT/EXPORT NON TERMINE'')') KNUMER
    CALL LFIEMS_FORT                                  &
&                   (LFI, KNUMER,INIMES,IREP,.FALSE., &
&                    CLMESS,CLNSPR,CLACTI)
  ENDIF
!
  DO J=1,LFI%NUIMEX
!
  IF (LFI%NINIEX(J).EQ.IRANIE) THEN
    IPOSNU=J
    GOTO 712
  ENDIF
!
  ENDDO
!
  IREP=-16
  GOTO 1001
!
712 CONTINUE
!
  LFI%MNUIEX(IRANG)=LFI%JPNIL
  LFI%NUIMEX=LFI%NUIMEX-1
!
  DO J=IPOSNU,LFI%NUIMEX
  LFI%NINIEX(J)=LFI%NINIEX(J+1)
  ENDDO
!
ENDIF
!
 IF (LFI%LMULTI) THEN
   CALL LFIVER_FORT                               &
&                  (LFI, LFI%VERRUE(IRANG),'OFF')
   CALL LFIVER_FORT                               &
&                  (LFI, LFI%VERRUE(IRANG),'REL')
 ENDIF
!
LLVERF=.FALSE.
IREP=IREPX
GOTO 1001
!**
!     9.  - CI-DESSOUS, ETIQUETTES DE BRANCHEMENT EN CAS D'ERREUR E/S.
!-----------------------------------------------------------------------
!
903 CONTINUE
CLACTI='WRITE'
GOTO 909
!
904 CONTINUE
CLACTI='READ'
GOTO 909
!
905 CONTINUE
CLACTI='CLOSE'
!
909 CONTINUE
IF (INAPHY.NE.0) LFI%NUMAPH(IRANG)=INAPHY
!
!      AU CAS OU, ON FORCE LE CODE-REPONSE ENTREE/SORTIE A ETRE POSITIF.
!
IREP=ABS (IREP)
!**
!    10.  -  PHASE TERMINALE : MESSAGERIE, AVEC "ABORT" EVENTUEL,
!            VIA LE SOUS-PROGRAMME "LFIEMS" .
!-----------------------------------------------------------------------
!
1001 CONTINUE
KREP=IREP
LLFATA=LLMOER (IREP,IRANG)
!
IF (LLFATA) THEN
  INIMES=2
ELSE
  INIMES=IXNIMS (IRANG)
ENDIF
!
 IF (LLVERF) CALL LFIVER_FORT                               &
&                            (LFI, LFI%VERRUE(IRANG),'OFF')
 IF (LLVERG) CALL LFIVER_FORT                        &
&                            (LFI, LFI%VERGLA,'OFF')
!
IF (.NOT.LLFATA.AND.INIMES.EQ.0)  THEN 
  IF (LHOOK) CALL DR_HOOK('LFIFER_FORT',1,ZHOOK_HANDLE)
  RETURN
ENDIF
!
IF (INIMES.EQ.2) THEN
  WRITE (UNIT=CLMESS,FMT='(''KREP='',I4,'', KNUMER='',I3, &
&         '', CDSTTC='''''',A,'''''''')')                  &
&      KREP,KNUMER,CLSTTC(:ILSTTU)
  CALL LFIEMS_FORT                                 &
&                 (LFI, KNUMER,INIMES,IREP,LLFATA, &
&                  CLMESS,CLNSPR,CLACTI)
ENDIF
!
!       LA MESSAGERIE QUI SUIT N'EST PAS EMISE EN CAS D'ERREUR FATALE.
!
IF (INIMES.GE.1.AND.(IREP.EQ.0.OR.IREP.EQ.-19)) THEN
  CLAUXI='   '
!
  IF (LFI%LFRANC) THEN
    WRITE (UNIT=CLMESS,FMT='(''Unite'',I3, &
&           '' traitee, Fichier'',A)')      &
&            KNUMER,CLAUXI
  ELSE
    WRITE (UNIT=CLMESS,FMT='(''Unit'',I3, &
&           '' processed, File'',A)')      &
&            KNUMER,CLAUXI
  ENDIF
!
  IDECAL=INT (INDEX (CLMESS,CLAUXI), JPLIKB)
!
  IF (INBALO.EQ.0) THEN
!
    IF (LFI%LFRANC) THEN
      CLMESS(IDECAL+1:)='*VIDE*'//CLAUXI
    ELSE
      CLMESS(IDECAL+1:)='*EMPTY*'//CLAUXI
    ENDIF
!
    IDECAL=IDECAL+INT (INDEX (CLMESS(IDECAL+1:),CLAUXI), JPLIKB)
  ENDIF
!
  IF (LFI%LFRANC) THEN
!
    IF (LFI%LNOUFI(IRANG)) THEN
      CLMESS(IDECAL+1:)='$CREE$ &'//CLAUXI
    ELSEIF (LLECRD) THEN
      CLMESS(IDECAL+1:)='$MODIFIE$ &'//CLAUXI
    ELSE
      CLMESS(IDECAL+1:)='non modifie &'//CLAUXI
    ENDIF
!
    IDECAL=IDECAL+INT (INDEX (CLMESS(IDECAL+1:),CLAUXI), JPLIKB)
!
    IF (.NOT.LLSTTU) THEN
      CLMESS(IDECAL+1:)='FERME'//CLAUXI
    ELSEIF (CLSTTC.EQ.'KEEP') THEN
      CLMESS(IDECAL+1:)='GARDE'//CLAUXI
    ELSE
      CLMESS(IDECAL+1:)='*RELACHE*'//CLAUXI
    ENDIF
!
    IF (LFI%LNOUFI(IRANG).OR.LLECRD) THEN
      IDECAL=IDECAL+INT (INDEX (CLMESS(IDECAL+1:),CLAUXI), JPLIKB)
      WRITE (UNIT=CLMESS(IDECAL+1:),                              &
&             FMT='(''a'',I9.6,''_'',I6.6,                         &
&       '','',I7,'' Articles de donnees,'',I9,'' mots en tout'')') &
&       LFI%MDES1D(IXM(LFI%JPDDMG,IRANG)),                         &
&       LFI%MDES1D(IXM(LFI%JPHDMG,IRANG)),                         &
&       INALDO,ILARPH*LFI%MDES1D(IXM(LFI%JPNAPH,IRANG))
    ENDIF
!
  ELSE
!
    IF (LFI%LNOUFI(IRANG)) THEN
      CLMESS(IDECAL+1:)='$CREATED$ &'//CLAUXI
    ELSEIF (LLECRD) THEN
      CLMESS(IDECAL+1:)='$MODIFIED$ &'//CLAUXI
    ELSE
      CLMESS(IDECAL+1:)='not modified &'//CLAUXI
    ENDIF
!
    IDECAL=IDECAL+INT (INDEX (CLMESS(IDECAL+1:),CLAUXI), JPLIKB)
!
    IF (.NOT.LLSTTU) THEN
      CLMESS(IDECAL+1:)='CLOSED'//CLAUXI
    ELSEIF (CLSTTC.EQ.'KEEP') THEN
      CLMESS(IDECAL+1:)='KEPT'//CLAUXI
    ELSE
      CLMESS(IDECAL+1:)='*RELEASED*'//CLAUXI
    ENDIF
!
    IF (LFI%LNOUFI(IRANG).OR.LLECRD) THEN
      IDECAL=IDECAL+INT (INDEX (CLMESS(IDECAL+1:),CLAUXI), JPLIKB)
      WRITE (UNIT=CLMESS(IDECAL+1:),FMT='(''at'',I9.6,''_'',   &
& I6.6,'','',I7,'' data Records,'',I9,'' words for a whole'')') &
&       LFI%MDES1D(IXM(LFI%JPDDMG,IRANG)),                      &
&       LFI%MDES1D(IXM(LFI%JPHDMG,IRANG)),                      &
&       INALDO,ILARPH*LFI%MDES1D(IXM(LFI%JPNAPH,IRANG))
    ENDIF
!
  ENDIF
!
  CALL LFIEMS_FORT                                  &
&                 (LFI, KNUMER,INIMES,IREP,.FALSE., &
&                  CLMESS,CLNSPR,CLACTI)
!
  IF (LFI%LFRANC) THEN
    INLNOM=MIN (LFI%NLNOMF(IRANG),LFI%JPLFIX, &
&                INT (LEN (CLMESS), JPLIKB)-6)
    CLMESS='Nom='''//LFI%CNOMFI(IRANG)(1:INLNOM)//''''
  ELSE
    INLNOM=MIN (LFI%NLNOMF(IRANG),LFI%JPLFIX, &
&                INT (LEN (CLMESS), JPLIKB)-7)
    CLMESS='Name='''//LFI%CNOMFI(IRANG)(1:INLNOM)//''''
  ENDIF
!
  CALL LFIEMS_FORT                                  &
&                 (LFI, KNUMER,INIMES,IREP,.FALSE., &
&                  CLMESS,CLNSPR,CLACTI)
!
  IF (LFI%CNOMSY(IRANG)(1:LFI%NLNOMS(IRANG)).NE.   &
&      LFI%CNOMFI(IRANG)(1:LFI%NLNOMF(IRANG))) THEN
!
    IF (LFI%LFRANC) THEN
      INLNOM=MIN (LFI%NLNOMS(IRANG),LFI%JPLFIX,  &
&                  INT (LEN (CLMESS), JPLIKB)-14)
      CLMESS='Nom SYSTEME='''//                    &
&                 LFI%CNOMSY(IRANG)(1:INLNOM)//''''
    ELSE
      INLNOM=MIN (LFI%NLNOMS(IRANG),LFI%JPLFIX,  &
&                  INT (LEN (CLMESS), JPLIKB)-14)
      CLMESS='SYSTEM Name='''//                     &
&                  LFI%CNOMSY(IRANG)(1:INLNOM)//''''
    ENDIF
!
    CALL LFIEMS_FORT                                  &
&                   (LFI, KNUMER,INIMES,IREP,.FALSE., &
&                    CLMESS,CLNSPR,CLACTI)
  ENDIF
!
ENDIF
!
IF (LHOOK) CALL DR_HOOK('LFIFER_FORT',1,ZHOOK_HANDLE)

CONTAINS

#include "lficom2.ixc.h"
#include "lficom2.ixm.h"
#include "lficom2.ixnims.h"
#include "lficom2.llmoer.h"

SUBROUTINE CLOSEC (KREP, CDSTTC)
USE PARKIND1, ONLY : JPIM

INTEGER (KIND=JPLIKB) KREP
CHARACTER (LEN=*)     CDSTTC

INTEGER (KIND=JPIM) IREP4

KREP=0

CALL FI_FCLOSE (IREP4, LFI%YLFIC (IRANG)%N_C_FPDESC)
IF (IREP4 /= 0) THEN
  CALL FI_ERRNO (IREP4)
  KREP=IREP4
ENDIF

IF (CDSTTC .EQ. 'DELETE') CALL FI_UNLINK (IREP4, TRIM (LFI%CNOMFI(IRANG)))

LFI%YLFIC (IRANG)%N_C_FPDESC = 0
LFI%YLFIC (IRANG)%N_C_OFFSET = 0
LFI%YLFIC (IRANG)%L_C_BTSWAP = .FALSE.
NULLIFY (LFI%YLFIC (IRANG)%CNOMFI)

END SUBROUTINE CLOSEC

END SUBROUTINE LFIFER_FORT



! Oct-2012 P. Marguinaud 64b LFI
SUBROUTINE LFIFER64              &
&           (KREP, KNUMER, CDSTTC)
USE LFIMOD, ONLY : LFI => LFICOM_DEFAULT, &
&                   LFICOM_DEFAULT_INIT,   &
&                   NEW_LFI_DEFAULT
USE LFI_PRECISION
IMPLICIT NONE
! Arguments
INTEGER (KIND=JPLIKB)  KREP                                   !   OUT
INTEGER (KIND=JPLIKB)  KNUMER                                 ! IN   
CHARACTER (LEN=*)      CDSTTC                                 ! IN   

IF (.NOT. LFICOM_DEFAULT_INIT) CALL NEW_LFI_DEFAULT ()

CALL LFIFER_FORT                       &
&           (LFI, KREP, KNUMER, CDSTTC)

END SUBROUTINE LFIFER64

SUBROUTINE LFIFER                &
&           (KREP, KNUMER, CDSTTC)
USE LFIMOD, ONLY : LFI => LFICOM_DEFAULT, &
&                   LFICOM_DEFAULT_INIT,   &
&                   NEW_LFI_DEFAULT
USE LFI_PRECISION
IMPLICIT NONE
! Arguments
INTEGER (KIND=JPLIKM)  KREP                                   !   OUT
INTEGER (KIND=JPLIKM)  KNUMER                                 ! IN   
CHARACTER (LEN=*)      CDSTTC                                 ! IN   

IF (.NOT. LFICOM_DEFAULT_INIT) CALL NEW_LFI_DEFAULT ()

CALL LFIFER_MT                        &
&           (LFI, KREP, KNUMER, CDSTTC)

END SUBROUTINE LFIFER

SUBROUTINE LFIFER_MT                  &
&           (LFI, KREP, KNUMER, CDSTTC)
USE LFIMOD, ONLY : LFICOM
USE LFI_PRECISION
IMPLICIT NONE
! Arguments
TYPE (LFICOM)          LFI                                    ! INOUT
INTEGER (KIND=JPLIKM)  KREP                                   !   OUT
INTEGER (KIND=JPLIKM)  KNUMER                                 ! IN   
CHARACTER (LEN=*)      CDSTTC                                 ! IN   
! Local integers
INTEGER (KIND=JPLIKB)  IREP                                   !   OUT
INTEGER (KIND=JPLIKB)  INUMER                                 ! IN   
! Convert arguments

INUMER     = INT (    KNUMER, JPLIKB)

CALL LFIFER_FORT                       &
&           (LFI, IREP, INUMER, CDSTTC)

KREP       = INT (      IREP, JPLIKM)

END SUBROUTINE LFIFER_MT

!INTF KREP            OUT 
!INTF KNUMER        IN    
!INTF CDSTTC        IN    
