MODULE LOCAL_TRAFOS
!**** LOCAL_TRAFOS transform localized quantities to other quantities

!     Purpose.
!     --------
!     Transform wind speed, direction to components and vice versa
!     Other functions also: value extracted from an array with an index
!        defined from a second array

!**   Interface.
!     ----------
!        USE LOCAL_TRAFOS

!     Author.
!     -------
!        H. Hersbach     ECMWF

!     Modifications.
!     --------------
!        Original: 2007-04-11

!        2009-09-04 C. Payan IVALFROMIDX added
!            allows to match an integer in an array from the index of an 
!            other integer's array

!     ------------------------------------------------------------------


USE PARKIND1  ,ONLY : JPIM, JPRM, JPRD
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

IMPLICIT NONE

PRIVATE

INTERFACE UCOM
  MODULE PROCEDURE UCOM4, UCOM8
END INTERFACE

INTERFACE VCOM
  MODULE PROCEDURE VCOM4, VCOM8
END INTERFACE

INTERFACE UV2FF
  MODULE PROCEDURE UV2FF4, UV2FF8
END INTERFACE

INTERFACE UV2DD
  MODULE PROCEDURE UV2DD4, UV2DD8
END INTERFACE

PUBLIC UCOM, VCOM, UV2FF, UV2DD, IVALFROMIDX

REAL(KIND=JPRM), PARAMETER :: PARC4=360._JPRM
REAL(KIND=JPRM), PARAMETER :: DEGCON4=3.14159265358979_JPRM/180._JPRM

REAL(KIND=JPRD), PARAMETER :: PARC8=360._JPRD
REAL(KIND=JPRD), PARAMETER :: DEGCON8=3.14159265358979_JPRD/180._JPRD


CONTAINS

!***********************************************************************
FUNCTION UCOM4(PDD,PFF) RESULT(UCOM)
REAL(KIND=JPRM)                  :: UCOM 
REAL(KIND=JPRM)   ,INTENT(IN)    :: PDD,PFF 
REAL(KIND=JPRD)                  :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:UCOM',0,ZHOOK_HANDLE)
UCOM = -PFF * SIN(PDD*DEGCON4)
IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:UCOM',1,ZHOOK_HANDLE)
END FUNCTION UCOM4

FUNCTION UCOM8(PDD,PFF) RESULT(UCOM)
REAL(KIND=JPRD)                  :: UCOM 
REAL(KIND=JPRD)   ,INTENT(IN)    :: PDD,PFF 
REAL(KIND=JPRD)                  :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:UCOM',0,ZHOOK_HANDLE)
UCOM = -PFF * SIN(PDD*DEGCON8)
IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:UCOM',1,ZHOOK_HANDLE)
END FUNCTION UCOM8


!***********************************************************************
FUNCTION VCOM4(PDD,PFF) RESULT(VCOM)
REAL(KIND=JPRM)                  :: VCOM 
REAL(KIND=JPRM)   ,INTENT(IN)    :: PDD,PFF 
REAL(KIND=JPRD)                  :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:VCOM',0,ZHOOK_HANDLE)
VCOM = -PFF * COS(PDD*DEGCON4)
IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:VCOM',1,ZHOOK_HANDLE)
END FUNCTION VCOM4

FUNCTION VCOM8(PDD,PFF) RESULT(VCOM)
REAL(KIND=JPRD)                  :: VCOM 
REAL(KIND=JPRD)   ,INTENT(IN)    :: PDD,PFF 
REAL(KIND=JPRD)                  :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:VCOM',0,ZHOOK_HANDLE)
VCOM = -PFF * COS(PDD*DEGCON8)
IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:VCOM',1,ZHOOK_HANDLE)
END FUNCTION VCOM8


!***********************************************************************
FUNCTION UV2FF4(PUU,PVV) RESULT(UV2FF)
REAL(KIND=JPRM)                  :: UV2FF 
REAL(KIND=JPRM)   ,INTENT(IN)    :: PUU,PVV
REAL(KIND=JPRD)                  :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:UV2FF',0,ZHOOK_HANDLE)
UV2FF =SQRT(PUU*PUU+PVV*PVV)
IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:UV2FF',1,ZHOOK_HANDLE)
END FUNCTION UV2FF4

FUNCTION UV2FF8(PUU,PVV) RESULT(UV2FF)
REAL(KIND=JPRD)                  :: UV2FF 
REAL(KIND=JPRD)   ,INTENT(IN)    :: PUU,PVV
REAL(KIND=JPRD)                  :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:UV2FF',0,ZHOOK_HANDLE)
UV2FF =SQRT(PUU*PUU+PVV*PVV)
IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:UV2FF',1,ZHOOK_HANDLE)
END FUNCTION UV2FF8


!***********************************************************************
FUNCTION UV2DD4(PUU,PVV) RESULT(UV2DD)
REAL(KIND=JPRM)                  :: UV2DD
REAL(KIND=JPRM)   ,INTENT(IN)    :: PUU,PVV
REAL(KIND=JPRD)                  :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:UV2DD',0,ZHOOK_HANDLE)

IF (PUU == 0._JPRM .AND. PVV == 0._JPRM) THEN
   UV2DD = 0.5_JPRM*PARC4
ELSE
   UV2DD =MOD(PARC4+ATAN2(-PUU,-PVV)/DEGCON4,PARC4)
ENDIF
      
IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:UV2DD',1,ZHOOK_HANDLE)
END FUNCTION UV2DD4

FUNCTION UV2DD8(PUU,PVV) RESULT(UV2DD)
REAL(KIND=JPRD)                  :: UV2DD
REAL(KIND=JPRD)   ,INTENT(IN)    :: PUU,PVV
REAL(KIND=JPRD)                  :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:UV2DD',0,ZHOOK_HANDLE)

IF (PUU == 0._JPRD .AND. PVV == 0._JPRD) THEN
   UV2DD = 0.5_JPRD*PARC8
ELSE
   UV2DD =MOD(PARC8+ATAN2(-PUU,-PVV)/DEGCON8,PARC8)
ENDIF
      
IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:UV2DD',1,ZHOOK_HANDLE)
END FUNCTION UV2DD8

!***********************************************************************
FUNCTION IVALFROMIDX(KTABVAL,KTABIDX,KCOND,KDEFAULT)
INTEGER(KIND=JPIM)               :: IVALFROMIDX
INTEGER(KIND=JPIM) ,DIMENSION(:) ,INTENT(IN)  :: KTABVAL ,KTABIDX
INTEGER(KIND=JPIM) ,INTENT(IN)   :: KCOND ,KDEFAULT
INTEGER(KIND=JPIM)               :: JJ ,IMXIDX
REAL(KIND=JPRD)                  :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:IVALFROMIDX',0,ZHOOK_HANDLE)

IVALFROMIDX=KDEFAULT
IMXIDX=MIN(SIZE(KTABIDX),SIZE(KTABVAL))

DO JJ=1,IMXIDX
  IF (KTABIDX(JJ)==KCOND) THEN
    IVALFROMIDX=KTABVAL(JJ)
    EXIT
  ENDIF
ENDDO

IF (LHOOK) CALL DR_HOOK('LOCAL_TRAFOS:IVALFROMIDX',1,ZHOOK_HANDLE)
END FUNCTION IVALFROMIDX

!***********************************************************************
END MODULE LOCAL_TRAFOS
