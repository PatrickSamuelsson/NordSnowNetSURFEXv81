MODULE FADUP_MOD

!**** *FADUP_MOD*  - Duplicate FA library

!     Author. 
!     ------- 
!      Philippe Marguinaud *METEO FRANCE*
!      Original : 11-09-2012


USE FA_MOD, ONLY : FA_COM, NEW_FA
USE YOMHOOK, ONLY : LHOOK, DR_HOOK
USE PARKIND1, ONLY : JPRB, JPIM

IMPLICIT NONE

TYPE FADUP_PARAMS

! librarie FA
  INTEGER (KIND=JPIM) :: IPXTRO
  INTEGER (KIND=JPIM) :: IPXLAT
  INTEGER (KIND=JPIM) :: IPXNIV
  INTEGER (KIND=JPIM) :: IPNXFA
  INTEGER (KIND=JPIM) :: IPNXCA
  
  INTEGER (KIND=JPIM) :: INGRIB 
  INTEGER (KIND=JPIM) :: INBPDG 
  INTEGER (KIND=JPIM) :: INBCSP 
  INTEGER (KIND=JPIM) :: ISTRON 
  INTEGER (KIND=JPIM) :: IPUILA 
  INTEGER (KIND=JPIM) :: IDMOPL

! fichier
  LOGICAL               :: LNOMM 
  LOGICAL               :: LOUVR
  CHARACTER*256         :: CNOMF 
  CHARACTER*32          :: CSTTU
  LOGICAL               :: LERFA 
  LOGICAL               :: LIMST 
  INTEGER (KIND=JPIM)   :: INIMES 
  CHARACTER*32          :: CNOMC
  INTEGER (KIND=JPIM)   :: IFACT

! cadre
  INTEGER (KIND=JPIM) :: ITYPTR 
  INTEGER (KIND=JPIM) :: ITRONC 
  INTEGER (KIND=JPIM) :: INLATI 
  INTEGER (KIND=JPIM) :: INXLON 
  INTEGER (KIND=JPIM) :: INIVER
  
  INTEGER (KIND=JPIM), POINTER :: INLOPA (:) => NULL ()
  INTEGER (KIND=JPIM), POINTER :: INOZPA (:) => NULL ()
  
  REAL (KIND=JPRB) :: ZSLAPO
  REAL (KIND=JPRB) :: ZCLOPO 
  REAL (KIND=JPRB) :: ZSLOPO
  REAL (KIND=JPRB) :: ZCODIL
  REAL (KIND=JPRB) :: ZREFER
  
  REAL (KIND=JPRB), POINTER :: ZSINLA (:) => NULL ()
  REAL (KIND=JPRB), POINTER :: ZAHYBR (:) => NULL ()
  REAL (KIND=JPRB), POINTER :: ZBHYBR (:) => NULL ()

  REAL (KIND=JPRB), POINTER :: XLAP1D  (:,:)   => NULL ()
  REAL (KIND=JPRB), POINTER :: XLAP1DA (:,:)   => NULL ()
  REAL (KIND=JPRB), POINTER :: XLAP2D  (:,:,:) => NULL ()
  REAL (KIND=JPRB), POINTER :: XLAP2DA (:,:,:) => NULL ()
  
  LOGICAL :: LGARD

! date
  INTEGER (KIND=JPIM), POINTER :: IDATEF (:) => NULL ()

! 
  INTEGER (KIND=JPIM) :: IULOUT_FA, IULOUT_LFI

  INTEGER (KIND=JPIM) :: IEXTERN = 0_JPIM
  CHARACTER (LEN=64)  :: CMODEL  = ''
  INTEGER (KIND=JPIM) :: NIDCEN  = 0_JPIM

END TYPE FADUP_PARAMS

PRIVATE :: FADUP1, FADUP2

CONTAINS

SUBROUTINE FADUP1 (YDFA, YDDFP, KUNIT, KERR)

! Record necessary FA parameters 

TYPE(FA_COM)                        :: YDFA
TYPE(FADUP_PARAMS)                  :: YDDFP
INTEGER (KIND=JPIM),   INTENT (IN)  :: KUNIT
INTEGER (KIND=JPIM),   INTENT (OUT) :: KERR

INTEGER (KIND=JPIM) :: IVAL

CHARACTER (LEN=64) :: CLMODEL

REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUP1',0,ZHOOK_HANDLE)

YDDFP%IPXTRO = INT (YDFA%JPXTRO)
YDDFP%IPXLAT = INT (YDFA%JPXLAT)
YDDFP%IPXNIV = INT (YDFA%JPXNIV)
YDDFP%IPNXFA = INT (YDFA%JPNXFA)
YDDFP%IPNXCA = INT (YDFA%JPNXCA)

CALL FAVORI_MT (YDFA,                                     &
&               YDDFP%INGRIB, YDDFP%INBPDG, YDDFP%INBCSP, &
&               YDDFP%ISTRON, YDDFP%IPUILA, YDDFP%IDMOPL)

CALL FAIOPT_MT (YDFA, KERR, KUNIT, YDDFP%LNOMM, YDDFP%CNOMF,         &
&               YDDFP%CSTTU, YDDFP%LERFA, YDDFP%LIMST, YDDFP%INIMES, &
&               YDDFP%CNOMC)

CALL LFIOFM_MT (YDFA%LFI, KERR, KUNIT, YDDFP%IFACT, YDDFP%LOUVR)

ALLOCATE (YDDFP%INLOPA (YDFA%JPXPAH), YDDFP%INOZPA (YDFA%JPXIND),   &
&         YDDFP%ZSINLA (YDFA%JPXGEO), YDDFP%ZAHYBR (0:YDFA%JPXNIV), &
&         YDDFP%ZBHYBR (0:YDFA%JPXNIV), YDDFP%IDATEF (YDFA%JPLDAT*2))

CALL FACIES_MT (YDFA,                                                   &
&               YDDFP%CNOMC,  YDDFP%ITYPTR, YDDFP%ZSLAPO, YDDFP%ZCLOPO, &
&               YDDFP%ZSLOPO, YDDFP%ZCODIL, YDDFP%ITRONC, YDDFP%INLATI, &
&               YDDFP%INXLON, YDDFP%INLOPA, YDDFP%INOZPA, YDDFP%ZSINLA, &
&               YDDFP%INIVER, YDDFP%ZREFER, YDDFP%ZAHYBR, YDDFP%ZBHYBR, &
&               YDDFP%LGARD)

CALL FADIEX_MT (YDFA, KERR, KUNIT, YDDFP%IDATEF)

YDDFP%IULOUT_FA  = YDFA%NULOUT
YDDFP%IULOUT_LFI = YDFA%LFI%NULOUT

IF (.NOT. YDFA%LIXLAP) THEN
  YDDFP%XLAP1D  => YDFA%XLAP1D
  YDDFP%XLAP1DA => YDFA%XLAP1DA
  YDDFP%XLAP2D  => YDFA%XLAP2D
  YDDFP%XLAP2DA => YDFA%XLAP2DA
ENDIF

CALL FAREGU (KUNIT, 'EXTERN', YDDFP%IEXTERN, 0_JPIM)
CLMODEL = 'CMODEL='
IVAL = 1_JPIM
CALL FAREGU (KUNIT, CLMODEL, IVAL, 0_JPIM)
YDDFP%CMODEL = CLMODEL (8:)

CALL FAREGU (KUNIT, 'IDCEN', YDDFP%NIDCEN, 0_JPIM)

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUP1',1,ZHOOK_HANDLE)

END SUBROUTINE FADUP1

SUBROUTINE FADUP2 (YDFA, YDDFP, KUNIT, KERR)

! Create FA library and re-open the file

TYPE(FA_COM)                     :: YDFA
TYPE(FADUP_PARAMS), INTENT (IN)  :: YDDFP
INTEGER (KIND=JPIM),INTENT (IN)  :: KUNIT
INTEGER (KIND=JPIM),INTENT (OUT) :: KERR

INTEGER (KIND=JPIM) :: INBARP, INBARI
REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUP2',0,ZHOOK_HANDLE)

INBARP = 0
INBARI = 0

CALL NEW_FA (YDFA, KERR,   YDDFP%IPXTRO, YDDFP%IPXLAT, &
&            YDDFP%IPXNIV, YDDFP%IPNXFA, YDDFP%IPNXCA)

YDFA%NULOUT     = YDDFP%IULOUT_FA
YDFA%LFI%NULOUT = YDDFP%IULOUT_LFI
YDFA%NIDCEN     = YDDFP%NIDCEN

CALL FAGIOT_MT (YDFA,                                       &
&               YDDFP%INGRIB, YDDFP%INBPDG, YDDFP%INBCSP,   &
&               YDDFP%ISTRON, YDDFP%IPUILA, YDDFP%IDMOPL)

CALL FACADE_MT (YDFA,                                                   &
&               YDDFP%CNOMC,  YDDFP%ITYPTR, YDDFP%ZSLAPO, YDDFP%ZCLOPO, &
&               YDDFP%ZSLOPO, YDDFP%ZCODIL, YDDFP%ITRONC, YDDFP%INLATI, &
&               YDDFP%INXLON, YDDFP%INLOPA, YDDFP%INOZPA, YDDFP%ZSINLA, &
&               YDDFP%INIVER, YDDFP%ZREFER, YDDFP%ZAHYBR, YDDFP%ZBHYBR, &
&               YDDFP%LGARD)

IF (YDDFP%LOUVR) THEN
  CALL LFIAFM_MT (YDFA%LFI, KERR, KUNIT, YDDFP%IFACT)
  CALL FAITOU_MT (YDFA, KERR, KUNIT, YDDFP%LNOMM, YDDFP%CNOMF,          &
&                 YDDFP%CSTTU, YDDFP%LERFA, YDDFP%LIMST, YDDFP%INIMES,  &
                  INBARP, INBARI, YDDFP%CNOMC)
ELSE
  CALL FANOUV_MT (YDFA, KERR, KUNIT, YDDFP%LNOMM, YDDFP%CNOMF,          &
&                 YDDFP%CSTTU, YDDFP%LERFA, YDDFP%LIMST, YDDFP%INIMES,  &
&                 INBARP, INBARI, YDDFP%CNOMC)
ENDIF

IF (.NOT. YDDFP%LOUVR) THEN
  CALL FANDAX_MT (YDFA, KERR, KUNIT, YDDFP%IDATEF)
ENDIF


YDFA%XLAP1D  => YDDFP%XLAP1D
YDFA%XLAP1DA => YDDFP%XLAP1DA
YDFA%XLAP2D  => YDDFP%XLAP2D
YDFA%XLAP2DA => YDDFP%XLAP2DA
YDFA%LIXLAP = &
      & .NOT.(ASSOCIATED (YDFA%XLAP1D)  &
      & .AND. ASSOCIATED (YDFA%XLAP1DA) &
      & .AND. ASSOCIATED (YDFA%XLAP2D)  &
      & .AND. ASSOCIATED (YDFA%XLAP2DA))

CALL FAREGU_MT (YDFA, KUNIT, 'EXTERN', YDDFP%IEXTERN, 1_JPIM)
CALL FAREGU_MT (YDFA, KUNIT, 'CMODEL='//TRIM (YDDFP%CMODEL), 1_JPIM, 1_JPIM)
CALL FAREGU_MT (YDFA, KUNIT, 'IDCEN', YDDFP%NIDCEN, 1_JPIM)

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUP2',1,ZHOOK_HANDLE)

END SUBROUTINE FADUP2

SUBROUTINE FADUPN1 (YDDFP, KUNIT)
USE OML_MOD, ONLY : OML_NUM_THREADS

TYPE (FADUP_PARAMS), POINTER     :: YDDFP (:)
INTEGER (KIND=JPIM), INTENT (IN) :: KUNIT

INTEGER (KIND=JPIM) :: NTID, ITID

REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPN1',0,ZHOOK_HANDLE)

NTID = OML_NUM_THREADS ()

ALLOCATE (YDDFP (NTID))

DO ITID = 1, NTID
  CALL FADUPU1 (YDDFP (ITID), KUNIT)
ENDDO

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPN1',1,ZHOOK_HANDLE)

END SUBROUTINE FADUPN1

SUBROUTINE FADUPN2 (YDFA, YDDFP, KUNIT)
USE OML_MOD, ONLY : OML_MY_THREAD
USE FA_MOD, ONLY : FA_COM_DEFAULT, FA_COM

TYPE (FA_COM),       POINTER     :: YDFA
TYPE (FADUP_PARAMS), POINTER     :: YDDFP (:)
INTEGER (KIND=JPIM), INTENT (IN) :: KUNIT

INTEGER (KIND=JPIM) :: ITID

REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPN2',0,ZHOOK_HANDLE)

ITID = OML_MY_THREAD ()

ALLOCATE (YDFA)

CALL FADUPU2 (YDFA, YDDFP (ITID), KUNIT)

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPN2',1,ZHOOK_HANDLE)

END SUBROUTINE FADUPN2

SUBROUTINE FADUPN3 (YDFA, YDDFP, KUNIT)
USE OML_MOD, ONLY : OML_MY_THREAD

TYPE (FA_COM),       POINTER     :: YDFA 
TYPE (FADUP_PARAMS), POINTER     :: YDDFP (:)
INTEGER (KIND=JPIM), INTENT (IN) :: KUNIT

INTEGER (KIND=JPIM) :: IREP
INTEGER (KIND=JPIM) :: ITID
REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPN3',0,ZHOOK_HANDLE)

ITID = OML_MY_THREAD ()

IF (YDDFP (ITID)%LOUVR) THEN
  CALL FAIRME_MT (YDFA, IREP, KUNIT, 'KEEP')
ELSE
  CALL FAIRNO_MT (YDFA, IREP, KUNIT, 'KEEP')
ENDIF

CALL FADUPU3 (YDFA, YDDFP (ITID))

DEALLOCATE (YDFA)

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPN3',1,ZHOOK_HANDLE)

END SUBROUTINE FADUPN3

SUBROUTINE FADUPN4 (YDDFP)
USE OML_MOD, ONLY : OML_NUM_THREADS

TYPE (FADUP_PARAMS), POINTER :: YDDFP (:)

INTEGER (KIND=JPIM) :: NTID, ITID

REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPN4',0,ZHOOK_HANDLE)

NTID = OML_NUM_THREADS ()

DO ITID = 1, NTID
  CALL FADUPU4 (YDDFP (ITID))
ENDDO

DEALLOCATE (YDDFP)

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPN4',1,ZHOOK_HANDLE)

END SUBROUTINE FADUPN4

SUBROUTINE FADUPU1 (YDDFP, KUNIT)
USE FA_MOD, ONLY : FA_COM_DEFAULT
USE LFIMOD, ONLY : LFICOM

TYPE (FADUP_PARAMS), INTENT (OUT) :: YDDFP
INTEGER (KIND=JPIM), INTENT (IN)  :: KUNIT

INTEGER (KIND=JPIM) :: IERR

REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPU1',0,ZHOOK_HANDLE)

CALL FADUP1 (FA_COM_DEFAULT, YDDFP, KUNIT, IERR)

YDDFP%IPNXFA = 1
YDDFP%IPNXCA = 1
YDDFP%LGARD  = .FALSE.

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPU1',1,ZHOOK_HANDLE)

END SUBROUTINE FADUPU1

SUBROUTINE FADUPU2 (YDFA, YDDFP, KUNIT)
USE LFIMOD, ONLY : NEW_LFI

TYPE (FA_COM)                    :: YDFA
TYPE (FADUP_PARAMS), INTENT (IN) :: YDDFP
INTEGER (KIND=JPIM), INTENT (IN) :: KUNIT

INTEGER (KIND=JPIM) :: IERR

REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPU2',0,ZHOOK_HANDLE)

ALLOCATE (YDFA%LFI)

CALL NEW_LFI (YDFA%LFI, IERR, KPNXFI=YDDFP%IFACT)

CALL FADUP2 (YDFA, YDDFP, KUNIT, IERR)
YDFA%LOPENMP = .FALSE.


IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPU2',1,ZHOOK_HANDLE)

END SUBROUTINE FADUPU2

SUBROUTINE FADUPU3 (YDFA, YDDFP)
USE LFIMOD, ONLY : FREE_LFI
USE FA_MOD, ONLY : FREE_FA

TYPE (FA_COM)                       :: YDFA
TYPE (FADUP_PARAMS), INTENT (INOUT) :: YDDFP

INTEGER (KIND=JPIM) :: IERR

REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPU3',0,ZHOOK_HANDLE)

IF (ASSOCIATED (YDFA%XLAP1D,  YDDFP%XLAP1D))  YDFA%XLAP1D  => NULL ()
IF (ASSOCIATED (YDFA%XLAP1DA, YDDFP%XLAP1DA)) YDFA%XLAP1DA => NULL ()
IF (ASSOCIATED (YDFA%XLAP2D,  YDDFP%XLAP2D))  YDFA%XLAP2D  => NULL ()
IF (ASSOCIATED (YDFA%XLAP2DA, YDDFP%XLAP2DA)) YDFA%XLAP2DA => NULL ()
YDFA%LIXLAP = .TRUE.

CALL FREE_FA (YDFA, IERR)
CALL FREE_LFI (YDFA%LFI, IERR)
DEALLOCATE (YDFA%LFI)
NULLIFY (YDFA%LFI)

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPU3',1,ZHOOK_HANDLE)

END SUBROUTINE FADUPU3

SUBROUTINE FADUPU4 (YDDFP)

TYPE (FADUP_PARAMS), INTENT (INOUT) :: YDDFP

REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPU4',0,ZHOOK_HANDLE)

DEALLOCATE (YDDFP%INLOPA, YDDFP%INOZPA, YDDFP%ZSINLA, &
&           YDDFP%ZAHYBR, YDDFP%ZBHYBR, YDDFP%IDATEF)

NULLIFY (YDDFP%INLOPA, YDDFP%INOZPA, YDDFP%ZSINLA,  &
&        YDDFP%ZAHYBR, YDDFP%ZBHYBR, YDDFP%IDATEF)

NULLIFY (YDDFP%XLAP1D, YDDFP%XLAP1DA, YDDFP%XLAP2D, YDDFP%XLAP2DA)

IF (LHOOK) CALL DR_HOOK ('FADUP_MOD:FADUPU4',1,ZHOOK_HANDLE)

END SUBROUTINE FADUPU4

END MODULE FADUP_MOD

