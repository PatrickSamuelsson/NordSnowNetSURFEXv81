      SUBROUTINE SBYTE_MF(KDEST,KSOURC,KOFSET,KBYTSZ)
      USE PARKIND1, ONLY : JPRB
      USE YOMHOOK , ONLY : LHOOK, DR_HOOK
      USE LFI_PRECISION
!*****
!*
!*    FUNCTION: STORE A SINGLE BIT FIELD FROM KSOURC INTO KDEST
!*
!*    INPUT   : KSOURC   = WORD CONTAINING  BIT FIELD RIGHT JUSTIFIED
!*              KDEST(1) = 1ST TARGET WORD
!*              KOFSET   = OFFSET IN BITS FOR START OF THE FIELD
!*              KBYTSZ   = LENGTH OF FIELD IN BITS ; .LE.WORD SIZE .....
!*
!*    OUTPUT  : KSOURC,KOFSET,KBYTSZ UNCHANGED
!*              KDEST(1) AND EVENTUALLY KDEST(2) CONTAIN FIELD
!*
!*    AUTHOR  : M.MIQUEU   08/1981 (REWRITTEN FROM J.MARTELLET'S)
!*
!*****
!
      IMPLICIT NONE
!
      INTEGER (KIND=JPLIKM) :: KOFSET
      INTEGER (KIND=JPLIKM) :: KBYTSZ
      INTEGER (KIND=JPLIKB) :: KSOURC
!
      INTEGER (KIND=JPLIKB) :: KDEST(*)
!
      INTEGER (KIND=JPLIKM) :: INBPW, ISH1, ISH2, ISH3
!
      REAL(KIND=JPRB) :: ZHOOK_HANDLE
      IF (LHOOK) CALL DR_HOOK('SBYTE_MF',0,ZHOOK_HANDLE)
      INBPW=64
      ISH1=KOFSET+KBYTSZ-INBPW
!
      IF (ISH1.LE.0) THEN
!
!     BYTE  DOES NOT SPAN WORDS
!
        KDEST(1)=ISHFTC(IOR(ISHFT(ISHFTC(KDEST(1),KOFSET,               &
     &           BIT_SIZE(KDEST(1))),KBYTSZ),                           &
     &           IBITS (KSOURC,0,KBYTSZ)),-ISH1,                        &
     &           BIT_SIZE(IOR(ISHFT(ISHFTC(KDEST(1),KOFSET,             &
     &           BIT_SIZE(KDEST(1))),KBYTSZ),IBITS (KSOURC,0,KBYTSZ))))
!
      ELSE
!
!     BYTE SPANS 2 WORDS
!
        KDEST(1)=IOR(ISHFTC(ISHFT(KDEST(1),KOFSET-INBPW),INBPW-KOFSET,  &
     &           BIT_SIZE(ISHFT(KDEST(1),KOFSET-INBPW))),               &
     &           ISHFT(IBITS(KSOURC,0,KBYTSZ),-ISH1))
!
        KDEST(2)=ISHFTC(IOR(ISHFT(KDEST(2),ISH1),IBITS(KSOURC,0,ISH1)), &
     &           -ISH1,BIT_SIZE(IOR(ISHFT(KDEST(2),ISH1),               &
     &           IBITS(KSOURC,0,ISH1))))
!
      ENDIF
!
      IF (LHOOK) CALL DR_HOOK('SBYTE_MF',1,ZHOOK_HANDLE)
      ENDSUBROUTINE SBYTE_MF
