! Oct-2012 P. Marguinaud 64b LFI
! Jan-2011 P. Marguinaud Thread-safe LFI

SUBROUTINE LFINEG_FORT               &
&                     (LFI, KNIVAU )
USE LFIMOD, ONLY : LFICOM
USE PARKIND1, ONLY : JPRB
USE YOMHOOK , ONLY : LHOOK, DR_HOOK
USE LFI_PRECISION
IMPLICIT NONE
!****
!        CE SOUS-PROGRAMME SE CHARGE DE METTRE LE NIVEAU GLOBAL D'ERREUR
!     FATALE (*LFI%NERFAG*) A LA VALEUR KNIVAU . PAR  DEFAUT, LFI%NERFAG VAUT 1.
!**
!        ARGUMENT : KNIVAU (ENTREE) ==> NIVEAU GLOBAL D'ERREUR FATALE.
!                                       VALEURS POSSIBLES
!
!     0 : RENDRE FATALE TOUTE ERREUR DETECTEE, MEME SI ELLE CORRESPOND
!         A UN FICHIER OUVERT AVEC L'OPTION "PAS D'ERREUR FATALE".
!     1 : NE REND FATALES QUE CERTAINES ERREURS "GLOBALES", C'EST-A-DIRE
!         NON RELIABLES A UN FICHIER OUVERT, ET LES ERREURS SUR LES  FI-
!         CHIERS OUVERTS AVEC L'OPTION "ERREUR FATALE" (MODE PAR DEFAUT)
!     2 : PASSER OUTRE TOUTE ERREUR DETECTEE, MEME SI ELLE CORRESPOND
!         A UN FICHIER OUVERT AVEC L'OPTION "ERREUR FATALE".
!         NEANMOINS, LE CODE-REPONSE EVENTUEL NE SERA PAS ZERO.
!
!
TYPE(LFICOM) :: LFI
INTEGER (KIND=JPLIKB) KNIVAU, INIMES, IREP, INUMER
CHARACTER(LEN=LFI%JPLSPX) CLNSPR
CHARACTER(LEN=LFI%JPLMES) CLMESS
CHARACTER(LEN=LFI%JPLFTX) CLACTI
LOGICAL LLFATA

!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
IF (LHOOK) CALL DR_HOOK('LFINEG_FORT',0,ZHOOK_HANDLE)
CLACTI=''
IF (LFI%LFINEG_LLPREA) THEN
  CALL LFIINI_FORT                 &
&                 (LFI, 2_JPLIKB )
  LFI%LFINEG_LLPREA=.FALSE.
ENDIF
!
IF (KNIVAU.GE.0.AND.KNIVAU.LE.2) THEN
  LFI%NERFAG=KNIVAU
  IREP=0
ELSE
  IREP=-2
ENDIF
!
LLFATA=IREP.NE.0.AND.LFI%NERFAG.NE.2
!
IF (LLFATA) THEN
  INIMES=2
ELSEIF (IREP.NE.0) THEN
  INIMES=0
ELSEIF (LFI%NIMESG.EQ.2) THEN
  INIMES=2
ELSE
  IF (LHOOK) CALL DR_HOOK('LFINEG_FORT',1,ZHOOK_HANDLE)
  RETURN
ENDIF
!
INUMER=LFI%JPNIL
CLNSPR='LFINEG'
!
IF (MAX (INIMES,LFI%NIMESG).EQ.2) THEN
!
  IF (LFI%LFRANC) THEN
    WRITE (UNIT=CLMESS,                              &
&           FMT='(''KNIVAU='',I5,'', CODE INTERNE='', &
&           I4)') KNIVAU,IREP
  ELSE
    WRITE (UNIT=CLMESS,                               &
&           FMT='(''KNIVAU='',I5,'', INTERNAL CODE='', &
&           I4)') KNIVAU,IREP
  ENDIF
!
  IF (INIMES.NE.2) CALL LFIEMS_FORT                              &
&                                  (LFI, INUMER,LFI%NIMESG,IREP, &
&                                   .FALSE.,CLMESS,              &
&                                   CLNSPR,CLACTI)
ENDIF
!
CALL LFIEMS_FORT                                 &
&               (LFI, INUMER,INIMES,IREP,LLFATA, &
&                CLMESS,CLNSPR,CLACTI)
!
IF (LHOOK) CALL DR_HOOK('LFINEG_FORT',1,ZHOOK_HANDLE)
END SUBROUTINE LFINEG_FORT



! Oct-2012 P. Marguinaud 64b LFI
SUBROUTINE LFINEG64           &
&           (KNIVAU)
USE LFIMOD, ONLY : LFI => LFICOM_DEFAULT, &
&                   LFICOM_DEFAULT_INIT,   &
&                   NEW_LFI_DEFAULT
USE LFI_PRECISION
IMPLICIT NONE
! Arguments
INTEGER (KIND=JPLIKB)  KNIVAU                                 ! IN   

IF (.NOT. LFICOM_DEFAULT_INIT) CALL NEW_LFI_DEFAULT ()

CALL LFINEG_FORT            &
&           (LFI, KNIVAU)

END SUBROUTINE LFINEG64

SUBROUTINE LFINEG             &
&           (KNIVAU)
USE LFIMOD, ONLY : LFI => LFICOM_DEFAULT, &
&                   LFICOM_DEFAULT_INIT,   &
&                   NEW_LFI_DEFAULT
USE LFI_PRECISION
IMPLICIT NONE
! Arguments
INTEGER (KIND=JPLIKM)  KNIVAU                                 ! IN   

IF (.NOT. LFICOM_DEFAULT_INIT) CALL NEW_LFI_DEFAULT ()

CALL LFINEG_MT             &
&           (LFI, KNIVAU)

END SUBROUTINE LFINEG

SUBROUTINE LFINEG_MT             &
&           (LFI, KNIVAU)
USE LFIMOD, ONLY : LFICOM
USE LFI_PRECISION
IMPLICIT NONE
! Arguments
TYPE (LFICOM)          LFI                                    ! INOUT
INTEGER (KIND=JPLIKM)  KNIVAU                                 ! IN   
! Local integers
INTEGER (KIND=JPLIKB)  INIVAU                                 ! IN   
! Convert arguments

INIVAU     = INT (    KNIVAU, JPLIKB)

CALL LFINEG_FORT            &
&           (LFI, INIVAU)


END SUBROUTINE LFINEG_MT

!INTF KNIVAU        IN    
