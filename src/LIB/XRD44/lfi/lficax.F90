! Oct-2012 P. Marguinaud 64b LFI
! Jan-2011 P. Marguinaud Thread-safe LFI
SUBROUTINE LFICAX_FORT                                            &
&                     (LFI, KREP, KRANG, KRGPIM, KARTEX, KRETIN )
USE LFIMOD, ONLY : LFICOM
USE PARKIND1, ONLY : JPRB
USE YOMHOOK , ONLY : LHOOK, DR_HOOK
USE LFI_PRECISION
IMPLICIT NONE
!****
!        SOUS-PROGRAMME *INTERNE* DU LOGICIEL DE FICHIERS INDEXES LFI;
!     RECHERCHE DE L'ARTICLE LOGIQUE *DE DONNEES* SUIVANT, DANS UNE
!     UNITE LOGIQUE DONNEE.
!**
!    ARGUMENTS : KREP   (SORTIE) ==> CODE-REPONSE DU SOUS-PROGRAMME;
!                KRANG  (ENTREE) ==> RANG ( DANS LA TABLE *LFI%NUMERO* )
!                                    DE L'UNITE LOGIQUE CONCERNEE;
!                KRGPIM (SORTIE) ==> RANG DANS LES TABLES LFI%CNOMAR,LFI%MLGPOS,
!                                    ETC. DE LA P.P.I OU FIGURE
!                                    L'ARTICLE ( 0 SI PAS TROUVE );
!                KARTEX (SORTIE) ==> RANG ( DANS LA PAGE D'INDEX ) DE L'
!                                    ARTICLE S'IL EXISTE ( 0 SINON );
!                KRETIN (SORTIE) ==> CODE-RETOUR INTERNE.
!
!
TYPE(LFICOM) :: LFI
INTEGER (KIND=JPLIKB) KREP, KRANG, KRGPIM, KARTEX 
INTEGER (KIND=JPLIKB) IRANG, INBALO, INALPP, INTPPI
INTEGER (KIND=JPLIKB) INPPIM, IDERGF, IRANGF, IRGPIF 
INTEGER (KIND=JPLIKB) IRGPIM, IRANGM, ILFORC, J
INTEGER (KIND=JPLIKB) INPILE, IARTIK, IARTIC, IRETOU 
INTEGER (KIND=JPLIKB) INIMES, INUMER, INALPI
INTEGER (KIND=JPLIKB) KRETIN, IRETIN
!
CHARACTER(LEN=LFI%JPLSPX) CLNSPR
CHARACTER(LEN=LFI%JPLMES) CLMESS
CHARACTER(LEN=LFI%JPLFTX) CLACTI
LOGICAL LLFATA

!**
!     1.  -  CONTROLES DES PARAMETRES D'APPEL, PUIS INITIALISATIONS.
!-----------------------------------------------------------------------
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
IF (LHOOK) CALL DR_HOOK('LFICAX_FORT',0,ZHOOK_HANDLE)
CLACTI=''
IRETOU=0
!
IF (KRANG.LE.0.OR.KRANG.GT.LFI%JPNXFI) THEN
  KREP=-16
  GOTO 1001
ENDIF
!
IRANG=KRANG
KREP=0
KRGPIM=0
KARTEX=0
INBALO=LFI%MDES1D(IXM(LFI%JPNALO,IRANG))
INALPP=LFI%JPNAPP*LFI%MFACTM(IRANG)
INTPPI=(INBALO-1+INALPP)/INALPP
INPPIM=LFI%NPPIMM(IRANG)
!
IF (LFI%NSUIVF(IRANG).EQ.LFI%JPNIL) THEN
!
!           ON N'A DONC PAS ENCORE APPELE CE SOUS-PROGRAMME POUR
!        RECHERCHER CET ARTICLE ( A PRIORI, VIA *LFICAS* ) .
!
  IF (LFI%NDERGF(IRANG).EQ.LFI%JPNIL) THEN
    IDERGF=0
  ELSE
    IDERGF=LFI%NDERGF(IRANG)
  ENDIF
!
  IF (IDERGF.GE.INBALO) THEN
    LFI%NSUIVF(IRANG)=0
    GOTO 1001
  ENDIF
!
  IRANGF=IDERGF+1
!
ELSEIF (LFI%NSUIVF(IRANG).EQ.0) THEN
!
!        PLUS D'ARTICLE LOGIQUE A LIRE "SEQUENTIELLEMENT".
!
  GOTO 1001
ELSEIF (LFI%NDERGF(IRANG).EQ.LFI%JPNIL.OR.           &
&        LFI%NSUIVF(IRANG).GT.LFI%NDERGF(IRANG)) THEN
  IRANGF=LFI%NSUIVF(IRANG)
ELSE
  KREP=-16
  GOTO 1001
ENDIF
!**
!     2.  -  EXPLORATION DES (PAIRES DE) PAGES ET ARTICLES D'INDEX,
!            A LA RECHERCHE DE L'ARTICLE LOGIQUE DEMANDE,
!            DEFINI PAR SON RANG "A PRIORI" DANS LE FICHIER.
!            ( MAIS IL FAUT "SAUTER" LES TROUS )
!-----------------------------------------------------------------------
!*
!     2.1 -  RECHERCHE DANS LES PAGES D'INDEX .
!-----------------------------------------------------------------------
!
IRGPIF=1+(IRANGF-1)/INALPP
!
211 CONTINUE
!
IF (IRANGF.LE.INALPP) THEN
  IRGPIM=LFI%MRGPIM(1,IRANG)
  GOTO 215
ELSEIF (IRANGF.GT.INALPP*(INTPPI-1)) THEN
  IRGPIM=LFI%MRGPIM(LFI%NPODPI(IRANG),IRANG)
  GOTO 215
ENDIF
!
DO J=2,INPPIM
IRGPIM=LFI%MRGPIM(J,IRANG)
IF (LFI%MRGPIF(IRGPIM).EQ.IRGPIF) GOTO 215
ENDDO
!
!        MISE EN MEMOIRE DE L'ARTICLE D'INDEX "NOMS" CHERCHE.
!
ILFORC=1
INPILE=1
CALL LFIPIM_FORT                                       &
&               (LFI, KREP,IRANG,IRANGM,IRGPIM,IRGPIF, &
&                ILFORC,INPILE,IRETIN)
!
IF (IRETIN.EQ.1) THEN
  GOTO 903
ELSEIF (IRETIN.EQ.2) THEN
  GOTO 904
ELSEIF (IRETIN.NE.0) THEN
  GOTO 1001
ENDIF
!
INPPIM=MAX (INPPIM,IRANGM)
!
215 CONTINUE
IARTIK=IRANGF-INALPP*(IRGPIF-1)
INALPI=MIN (INALPP,INBALO-(IRGPIF-1)*INALPP)
!
!         ON CHERCHE LE PREMIER ARTICLE LOGIQUE *DE DONNEES* DE LA PAGE
!       D'INDEX, A PARTIR DU RANG *IARTIK* DANS CETTE PAGE.
!
DO J=IARTIK,INALPI
!
IF (LFI%CNOMAR(IXC(J,IRGPIM)).NE.' ') THEN
  IARTIC=J
  GOTO 220
ENDIF
!
ENDDO
!
!        CHOU BLANC POUR CETTE PAGE... A PRIORI, ON VA CHERCHER DANS
!     LA P.A.I. SUIVANTE, EN RANG DANS LE FICHIER.
!
IF (IRGPIF.LT.INTPPI) THEN
  IRANGF=INALPP*IRGPIF+1
  IRGPIF=IRGPIF+1
  GOTO 211
ENDIF
!
!    SI ON ARRIVE ICI, C'EST QUE LE DERNIER ARTICLE LOGIQUE EST UN TROU.
!
LFI%NSUIVF(IRANG)=0
GOTO 1001
!*
!     2.2 -  ARTICLE DE DONNEES REPERE, ON RENVOIE SES CARACTERISTIQUES.
!-----------------------------------------------------------------------
!
220 CONTINUE
KRGPIM=IRGPIM
KARTEX=IARTIC
LFI%NSUIVF(IRANG)=(IRGPIF-1)*INALPP+IARTIC
GOTO 1001
!**
!     9.  - CI-DESSOUS, ETIQUETTES DE BRANCHEMENT EN CAS D'ERREUR E/S.
!      AU CAS OU, ON FORCE LE CODE-REPONSE ENTREE/SORTIE A ETRE POSITIF.
!-----------------------------------------------------------------------
!
903 CONTINUE
IRETOU=1
CLACTI='WRITE'
GOTO 909
!
904 CONTINUE
IRETOU=2
CLACTI='READ'
!
909 CONTINUE
KREP=ABS (KREP)
!**
!    10.  -  PHASE TERMINALE : MESSAGERIE INTERNE EVENTUELLE,
!            VIA LE SOUS-PROGRAMME "LFIEMS", PUIS RETOUR.
!-----------------------------------------------------------------------
!
1001 CONTINUE
LLFATA=LLMOER (KREP,KRANG)
!
IF (KREP.EQ.0) THEN
  KRETIN=0
ELSEIF (KREP.GT.0) THEN
  KRETIN=IRETOU
ELSE
  KRETIN=3
ENDIF
!
IF (LFI%LMISOP.OR.LLFATA) THEN
  INUMER=LFI%NUMERO(KRANG)
  INIMES=2
  CLNSPR='LFICAX'
  WRITE (UNIT=CLMESS,FMT='(''KREP='',I4,'', KRANG='',I3,       &
&         '', KRGPIM='',I3,'', KARTEX='',I5,'', KRETIN='',I2)') &
&    KREP,KRANG,KRGPIM,KARTEX,KRETIN
  CALL LFIEMS_FORT                                  &
&                 (LFI, INUMER,INIMES,KREP,.FALSE., &
&                  CLMESS,CLNSPR,CLACTI)
ENDIF
!
IF (LHOOK) CALL DR_HOOK('LFICAX_FORT',1,ZHOOK_HANDLE)

CONTAINS

#include "lficom2.ixc.h"
#include "lficom2.ixm.h"
#include "lficom2.llmoer.h"

END SUBROUTINE LFICAX_FORT



! Oct-2012 P. Marguinaud 64b LFI
SUBROUTINE LFICAX64                             &
&           (KREP, KRANG, KRGPIM, KARTEX, KRETIN)
USE LFIMOD, ONLY : LFI => LFICOM_DEFAULT, &
&                   LFICOM_DEFAULT_INIT,   &
&                   NEW_LFI_DEFAULT
USE LFI_PRECISION
IMPLICIT NONE
! Arguments
INTEGER (KIND=JPLIKB)  KREP                                   !   OUT
INTEGER (KIND=JPLIKB)  KRANG                                  ! IN   
INTEGER (KIND=JPLIKB)  KRGPIM                                 !   OUT
INTEGER (KIND=JPLIKB)  KARTEX                                 !   OUT
INTEGER (KIND=JPLIKB)  KRETIN                                 !   OUT

IF (.NOT. LFICOM_DEFAULT_INIT) CALL NEW_LFI_DEFAULT ()

CALL LFICAX_FORT                                     &
&           (LFI, KREP, KRANG, KRGPIM, KARTEX, KRETIN)

END SUBROUTINE LFICAX64

SUBROUTINE LFICAX                               &
&           (KREP, KRANG, KRGPIM, KARTEX, KRETIN)
USE LFIMOD, ONLY : LFI => LFICOM_DEFAULT, &
&                   LFICOM_DEFAULT_INIT,   &
&                   NEW_LFI_DEFAULT
USE LFI_PRECISION
IMPLICIT NONE
! Arguments
INTEGER (KIND=JPLIKM)  KREP                                   !   OUT
INTEGER (KIND=JPLIKM)  KRANG                                  ! IN   
INTEGER (KIND=JPLIKM)  KRGPIM                                 !   OUT
INTEGER (KIND=JPLIKM)  KARTEX                                 !   OUT
INTEGER (KIND=JPLIKM)  KRETIN                                 !   OUT

IF (.NOT. LFICOM_DEFAULT_INIT) CALL NEW_LFI_DEFAULT ()

CALL LFICAX_MT                                       &
&           (LFI, KREP, KRANG, KRGPIM, KARTEX, KRETIN)

END SUBROUTINE LFICAX

SUBROUTINE LFICAX_MT                                 &
&           (LFI, KREP, KRANG, KRGPIM, KARTEX, KRETIN)
USE LFIMOD, ONLY : LFICOM
USE LFI_PRECISION
IMPLICIT NONE
! Arguments
TYPE (LFICOM)          LFI                                    ! INOUT
INTEGER (KIND=JPLIKM)  KREP                                   !   OUT
INTEGER (KIND=JPLIKM)  KRANG                                  ! IN   
INTEGER (KIND=JPLIKM)  KRGPIM                                 !   OUT
INTEGER (KIND=JPLIKM)  KARTEX                                 !   OUT
INTEGER (KIND=JPLIKM)  KRETIN                                 !   OUT
! Local integers
INTEGER (KIND=JPLIKB)  IREP                                   !   OUT
INTEGER (KIND=JPLIKB)  IRANG                                  ! IN   
INTEGER (KIND=JPLIKB)  IRGPIM                                 !   OUT
INTEGER (KIND=JPLIKB)  IARTEX                                 !   OUT
INTEGER (KIND=JPLIKB)  IRETIN                                 !   OUT
! Convert arguments

IRANG      = INT (     KRANG, JPLIKB)

CALL LFICAX_FORT                                     &
&           (LFI, IREP, IRANG, IRGPIM, IARTEX, IRETIN)

KREP       = INT (      IREP, JPLIKM)
KRGPIM     = INT (    IRGPIM, JPLIKM)
KARTEX     = INT (    IARTEX, JPLIKM)
KRETIN     = INT (    IRETIN, JPLIKM)

END SUBROUTINE LFICAX_MT

!INTF KREP            OUT 
!INTF KRANG         IN    
!INTF KRGPIM          OUT 
!INTF KARTEX          OUT 
!INTF KRETIN          OUT 
