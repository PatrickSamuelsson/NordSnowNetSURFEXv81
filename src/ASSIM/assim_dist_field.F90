!SFX_LIC Copyright 1994-2014 CNRS, Meteo-France and Universite Paul Sabatier
!SFX_LIC This is part of the SURFEX software governed by the CeCILL-C licence
!SFX_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt  
!SFX_LIC for details. version 1.
SUBROUTINE ASSIM_DIST_FIELD(HPROGRAM,PFIELD_GLOBAL,PFIELD_LOCAL)
  USE PARKIND1,        ONLY : JPIM, JPRB
  USE YOMHOOK,         ONLY : DR_HOOK, LHOOK
  USE MODD_ASSIM,      ONLY : LPIO
  USE MODD_SURFEX_MPI, ONLY : NPROC,NINDEX
  USE MODD_SURF_PAR,   ONLY : XUNDEF
  USE MODI_READ_AND_SEND_MPI
  IMPLICIT NONE
  CHARACTER(LEN=6),  INTENT(IN)  :: HPROGRAM
  REAL,DIMENSION(:), INTENT(IN)  :: PFIELD_GLOBAL
  REAL,DIMENSION(:), INTENT(OUT) :: PFIELD_LOCAL
  REAL,DIMENSION(:),ALLOCATABLE  :: ZWORK
  INTEGER                        :: ISIZE,JI
  
  REAL(KIND=JPRB) :: ZHOOK_HANDLE
  IF (LHOOK) CALL DR_HOOK('ASSIM_DIST_FIELD',0,ZHOOK_HANDLE)

  IF(HPROGRAM == 'AROME ') THEN
#ifdef SFX_ARO
    CALL ARO_GET_SIZE_GLOBAL(SIZE(PFIELD_LOCAL),ISIZE)
    IF ( LPIO ) THEN
      ALLOCATE(ZWORK(ISIZE))
      ZWORK(:)=PFIELD_GLOBAL(:)
    ELSE
      ALLOCATE(ZWORK(0))
    ENDIF
    CALL ARO_DIST_FIELD(SIZE(PFIELD_LOCAL),ISIZE,ZWORK,PFIELD_LOCAL)
    DEALLOCATE(ZWORK)
#else
    CALL ABOR1_SFX('ASSIM_DIST_FIELD: SFX_ARO should be defined while calling SURFEX from AROME')
#endif
  ELSE
    ISIZE=SIZE(NINDEX)
    IF ( SIZE(PFIELD_GLOBAL) /= ISIZE ) THEN
       WRITE(*,*) "SIZE(PFIELD_GLOBAL):",SIZE(PFIELD_GLOBAL),"ISIZE:",ISIZE
       CALL ABOR1_SFX('Mismatch in dimensions between global fields')
    ENDIF

    IF ( LPIO ) THEN
      ALLOCATE(ZWORK(SIZE(PFIELD_GLOBAL)))
      ZWORK(:)=PFIELD_GLOBAL(:)
    ELSE
      ALLOCATE(ZWORK(0))
    ENDIF

    ! Distribute ZWORK to all processors
    IF (NPROC>1) THEN
      CALL READ_AND_SEND_MPI(ZWORK(:),PFIELD_LOCAL(:))
    ELSE
       PFIELD_LOCAL(:)=ZWORK(:)
    ENDIF
    DEALLOCATE(ZWORK)
  ENDIF
  IF (LHOOK) CALL DR_HOOK('ASSIM_DIST_FIELD',1,ZHOOK_HANDLE)
END SUBROUTINE ASSIM_DIST_FIELD
