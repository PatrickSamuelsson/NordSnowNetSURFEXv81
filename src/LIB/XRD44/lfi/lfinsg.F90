! Oct-2012 P. Marguinaud 64b LFI
! Jan-2011 P. Marguinaud Thread-safe LFI

SUBROUTINE LFINSG_FORT               &
&                     (LFI, KNIVAU )
USE LFIMOD, ONLY : LFICOM
USE PARKIND1, ONLY : JPRB
USE YOMHOOK , ONLY : LHOOK, DR_HOOK
USE LFI_PRECISION
IMPLICIT NONE
!****
!        CE SOUS-PROGRAMME SE CHARGE DE METTRE LE NIVEAU GLOBAL
!     D'IMPRESSION DES STATISTIQUES A LA FERMETURE (*LFI%NISTAG*)
!     A LA VALEUR KNIVAU . PAR DEFAUT, LFI%NISTAG VAUT 1 .
!**
!        ARGUMENT : KNIVAU (ENTREE) ==> NIVEAU GLOBAL D'IMPRESSION DES
!                                       STATISTIQUES; VALEURS POSSIBLES
!
!     0 : NE PAS IMPRIMER LES STATISTIQUES A LA FERMETURE D'UN
!         FICHIER, MEME SI CELA AVAIT ETE DEMANDE LORS DE SON OUVERTURE.
!     1 : N'IMPRIMER LES STATISTIQUES A LA FERMETURE QUE SI CELA
!         AVAIT ETE DEMANDE LORS DE L'OUVERTURE (MODE PAR DEFAUT).
!     2 : IMPRIMER LES STATISTIQUES A LA FERMETURE D'UN FICHIER,
!         MEME SI LE FICHIER AVAIT ETE OUVERT SANS CETTE OPTION.
!
!
TYPE(LFICOM) :: LFI
INTEGER (KIND=JPLIKB) KNIVAU, INIMES, IREP, INUMER
CHARACTER(LEN=LFI%JPLSPX) CLNSPR
CHARACTER(LEN=LFI%JPLMES) CLMESS
CHARACTER(LEN=LFI%JPLFTX) CLACTI
LOGICAL LLFATA

!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
IF (LHOOK) CALL DR_HOOK('LFINSG_FORT',0,ZHOOK_HANDLE)
CLACTI=''
IF (LFI%LFINSG_LLPREA) THEN
  CALL LFIINI_FORT                 &
&                 (LFI, 2_JPLIKB )
  LFI%LFINSG_LLPREA=.FALSE.
ENDIF
!
IF (KNIVAU.GE.0.AND.KNIVAU.LE.2) THEN
  LFI%NISTAG=KNIVAU
  IREP=0
ELSE
  IREP=-2
ENDIF
!
!        MESSAGERIE EVENTUELLE, AVEC ABORT SI NECESSAIRE .
!
LLFATA=IREP.NE.0.AND.LFI%NERFAG.NE.2
!
IF (LLFATA) THEN
  INIMES=2
ELSEIF (IREP.NE.0) THEN
  INIMES=0
ELSEIF (LFI%NIMESG.EQ.2) THEN
  INIMES=2
ELSE
  IF (LHOOK) CALL DR_HOOK('LFINSG_FORT',1,ZHOOK_HANDLE)
  RETURN
ENDIF
!
INUMER=LFI%JPNIL
CLNSPR='LFINSG'
!
IF (MAX (INIMES,LFI%NIMESG).EQ.2) THEN
!
  IF (LFI%LFRANC) THEN
    WRITE (UNIT=CLMESS,                              &
&           FMT='(''KNIVAU='',I5,'', CODE INTERNE='', &
&           I4)') KNIVAU,IREP
  ELSE
    WRITE (UNIT=CLMESS,                               &
&           FMT='(''KNIVAU='',I5,'', INTERNAL CODE='', &
&           I4)') KNIVAU,IREP
  ENDIF
!
  IF (INIMES.NE.2) CALL LFIEMS_FORT                              &
&                                  (LFI, INUMER,LFI%NIMESG,IREP, &
&                                   .FALSE.,CLMESS,              &
&                                   CLNSPR,CLACTI)
ENDIF
!
CALL LFIEMS_FORT                                 &
&               (LFI, INUMER,INIMES,IREP,LLFATA, &
&                CLMESS,CLNSPR,CLACTI)
!
IF (LHOOK) CALL DR_HOOK('LFINSG_FORT',1,ZHOOK_HANDLE)
END SUBROUTINE LFINSG_FORT



! Oct-2012 P. Marguinaud 64b LFI
SUBROUTINE LFINSG64           &
&           (KNIVAU)
USE LFIMOD, ONLY : LFI => LFICOM_DEFAULT, &
&                   LFICOM_DEFAULT_INIT,   &
&                   NEW_LFI_DEFAULT
USE LFI_PRECISION
IMPLICIT NONE
! Arguments
INTEGER (KIND=JPLIKB)  KNIVAU                                 ! IN   

IF (.NOT. LFICOM_DEFAULT_INIT) CALL NEW_LFI_DEFAULT ()

CALL LFINSG_FORT            &
&           (LFI, KNIVAU)

END SUBROUTINE LFINSG64

SUBROUTINE LFINSG             &
&           (KNIVAU)
USE LFIMOD, ONLY : LFI => LFICOM_DEFAULT, &
&                   LFICOM_DEFAULT_INIT,   &
&                   NEW_LFI_DEFAULT
USE LFI_PRECISION
IMPLICIT NONE
! Arguments
INTEGER (KIND=JPLIKM)  KNIVAU                                 ! IN   

IF (.NOT. LFICOM_DEFAULT_INIT) CALL NEW_LFI_DEFAULT ()

CALL LFINSG_MT             &
&           (LFI, KNIVAU)

END SUBROUTINE LFINSG

SUBROUTINE LFINSG_MT             &
&           (LFI, KNIVAU)
USE LFIMOD, ONLY : LFICOM
USE LFI_PRECISION
IMPLICIT NONE
! Arguments
TYPE (LFICOM)          LFI                                    ! INOUT
INTEGER (KIND=JPLIKM)  KNIVAU                                 ! IN   
! Local integers
INTEGER (KIND=JPLIKB)  INIVAU                                 ! IN   
! Convert arguments

INIVAU     = INT (    KNIVAU, JPLIKB)

CALL LFINSG_FORT            &
&           (LFI, INIVAU)


END SUBROUTINE LFINSG_MT

!INTF KNIVAU        IN    
